\chapter{Properties of the two dimensional $\SL_2(F)$}\label{Ch5_PropertiesOfSLOverAlgClosedField}


\section{General Notation}

Throughout this paper, $F$ will denote an arbitrary algebraically closed field. 
The letter $p$ will be used to denote the characteristic of $F$. 
Recall that the definition of the characteristic of a field is:

\begin{definition}[Characteristic of a field]
    Let $F$ be a field, the characteristic of a field, denoted by $\textrm{char}(F) \in \N$, is the smallest natural number $p \in \N_0$ such that

    \[
    \underbrace{1 + \ldots + 1}_{p} = 0
    \]

    where in the case there is no such number then $p = 0$.
\end{definition}

\begin{example}
    $\Z /p\Z$ is a field of characteristic $\textrm{char}(\Z/p\Z) = p$ as $p \cdot 1 = 0$.
\end{example}

\begin{example}
    The field $\Q$ is a field with $\textrm{char}(\Q) = 0$ as $n \cdot 1 \ne 0$ for all $n \in \N \subset \Q$.
\end{example}

\begin{remark}[The characteristic is either prime or zero]
    The characteristic of a field is either a prime number or zero.
\end{remark}

Unless otherwise stated, the letters $\alpha, \beta, \gamma, \delta$ and $\sigma$ will denote elements of $F$; 
whereas $\delta$ and $\rho$ will denote elements of $F^\times$, where $F^\times$ are the invertible, or equivalently, non-zero elements of $F$.

\section{Subsets of $\SL_2(F)$}

In this chapter we make some useful observations about specific elements and subgroups of $\SL_2(F)$. 

First, we define the following elements of $\SL_2(F)$.

\subsubsection{Special matrices of $\SL_2(F)$}

\begin{definition}[The diagonal matrix of $SL_2(F)$]
\label{SpecialMatrices.d}
\lean{SpecialMatrices.d}
\leanok
    Given an element $\delta \in F^\times$ we define the diagonal matrix:
    \[
    d_\delta = \begin{bmatrix}
        \delta & 0\\
        0 & \delta^{-1}
    \end{bmatrix}
    \]
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def d (δ : Fˣ) : SL(2, F) :=
    ⟨!![(δ : F), (0 : F); (0 :F) , (δ⁻¹ : F)], by norm_num⟩
\end{verbatim}
\end{footnotesize}

\begin{remark}[Constructing a term of $\SL_2(F)$]
    To construct a term of $\SL_2(F)$ one has to bear in mind that the special linear group is defined to
    be a subtype of matrices with determinant one, thus, in the \textit{anonymous constructor}
    one has to provide:

    \begin{itemize}
        \item The term of type \texttt{Matrix (Fin 2) (Fin 2) F}.
        \item The proof term that the matrix \texttt{Matrix (Fin 2) (Fin 2) F} has determinant one.
    \end{itemize}
\end{remark}

\begin{definition}[The shear matrix of $SL_2(F)$]
\label{SpecialMatrices.s}
\lean{SpecialMatrices.s}
\leanok
    Given an element $\delta \in F$ we define the shear matrix:
    \[
    s_\sigma  = \begin{bmatrix}
    1 & 0\\
    \sigma & 1
    \end{bmatrix}
    \]
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def s (σ : F) : SL(2,F) :=
    ⟨!![1, 0; σ, 1], by simp⟩
\end{verbatim}
\end{footnotesize}

We record the nice property of $s_\sigma$


\begin{definition}[Rotation by $\pi / 2$ radians matrix]
\label{SpecialMatrices.w}
\lean{SpecialMatrices.w}
\leanok
 We denote the matrix which corresponds to a rotation by $\pi / 2$ radians to be:
 \[
 w = \begin{bmatrix}
    0 & -1\\
    1 & 0
 \end{bmatrix}
 \]
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def w : SL(2, F) :=
    ⟨!![0,1;-1,0], by norm_num⟩
\end{verbatim}
\end{footnotesize}

The matrices $d$, $s$ and $w$ satisfy the following relations:


\begin{lemma}[Closure of $D$ under multiplication]
\label{SpecialMatrices.d_mul_d_eq_d_mul}
\uses{SpecialMatrices.d}
\lean{SpecialMatrices.d_mul_d_eq_d_mul}
\leanok
For any $\delta, \rho \in F^\times$ we have that
\[
d_\delta d_\rho = d_{\delta\rho}
\]
\end{lemma}
\begin{proof}
\leanok
    We verify by matrix multiplication that indeed:

    \begin{equation*}
        d_\delta d_\rho = \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \begin{bmatrix} \rho & 0 \\ 0 & \rho^{-1} \end{bmatrix} = \begin{bmatrix} \delta \rho & 0 \\ 0 & \delta^{-1} \rho^{-1} \end{bmatrix} = d_{\delta \rho}.
    \end{equation*}
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma d_mul_d_eq_d_mul (δ ρ : Fˣ) : d δ * d ρ = d (δ * ρ) := by
    ext <;> simp [d, mul_comm]
\end{verbatim}
\end{footnotesize}

\begin{lemma}[Closure of $S$ under multiplication]
\label{SpecialMatrices.s_mul_s_eq_s_add}
\uses{SpecialMatrices.s}
\lean{SpecialMatrices.s_mul_s_eq_s_add}
\leanok
    For any $\sigma, \gamma \in F$ we have that
    \[
    s_\sigma s_\gamma = s_{\sigma + \gamma}.
    \]
\end{lemma}
\begin{proof}
\leanok
    We verify by matrix multiplication that indeed:
\begin{equation*}
    s_\sigma s_\gamma = \begin{bmatrix} 1 & 0 \\ \sigma & 1 \end{bmatrix} \begin{bmatrix} 1 & 0 \\ \gamma & 1 \end{bmatrix} = \begin{bmatrix} 1 & 0 \\ \sigma + \gamma & 1 \end{bmatrix} = s_{\sigma + \gamma}.
\end{equation*}
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma s_mul_s_eq_s_add (σ μ : F) : s σ * s μ = s (σ + μ) := by
    ext <;> simp [s]
\end{verbatim}
\end{footnotesize}

\begin{lemma}
    \label{SpecialMatrices.s_pow_eq_s_mul}
    \uses{SpecialMatrices.s}
    \lean{SpecialMatrices.s_pow_eq_s_mul}
    \leanok
    For any $\sigma \in F$ and for any $n \in \N$, we have that $s_\sigma^n = s_{n \cdot \sigma}$
\end{lemma}
\begin{proof}
\uses{SpecialMatrices.s_mul_s_eq_s_add}
\leanok
    We prove this by induction, indeed for $n= 0$ the identity holds trivially.

    Suppose $s_\sigma^n = \begin{bmatrix}
        1 & 0\\
        n \cdot \sigma & 0\end{bmatrix}$ then consider $s_\sigma^(n + 1)$. Since 

        \[
        s_\sigma^(n + 1) = s_\sigma^n s_\sigma = s_{n \cdot \sigma} s_\sigma = s_{(n + 1)\sigma}
        \]
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma s_pow_eq_s_mul (σ : F) (n : ℕ) : (s σ)^n = s (n • σ) := by
    induction n
    case zero => simp only [pow_zero, zero_smul, s_zero_eq_one]
    case succ k hk =>
      simp only [pow_add, hk, nsmul_eq_mul, pow_one, s_mul_s_eq_s_add, Nat.cast_add, Nat.cast_one]
      congr
      ring
\end{verbatim}
\end{footnotesize}

\begin{lemma}[Order of nontrivial $s_\sigma$ ]
    \label{SpecialMatrices.order_s_eq_char}
    \uses{SpecialMatrices.s}
    \lean{SpecialMatrices.order_s_eq_char}
    \leanok
    The order of $s_\sigma$ for any $\sigma \ne 0$ is $\textrm{char}(F)$
    \end{lemma}
    
    \begin{proof}
    \uses{SpecialMatrices.s_pow_eq_s_mul}
    \leanok
    Let $p$ denote the characteristic of the field, and let $\sigma \in F$, by \ref{SpecialMatrices.s_pow_eq_s_mul} we know that for any $s_\sigma^p = s_{p \cdot \sigma}$. 
    Since $p$ is the characteristic of the field $p \cdot \sigma = 0$, we have that $s_{p \cdot \sigma} = s_0 = I$
    \end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma order_s_eq_char {p : ℕ} [hp : Fact (Nat.Prime p)] [hC : CharP F p]
    (σ : F) (hσ : σ ≠ 0) : orderOf (s σ) = p := by
    refine orderOf_eq_prime ?hg ?hg1
    · simp only [s_pow_eq_s_mul, nsmul_eq_mul, CharP.cast_eq_zero, zero_mul, s_zero_eq_one]
    · contrapose! hσ
      exact (s_eq_one_iff σ).mp hσ
\end{verbatim}
\end{footnotesize}

\begin{lemma}
\label{SpecialMatrices.d_mul_s_mul_d_inv_eq_s}
\uses{SpecialMatrices.d, SpecialMatrices.s}
\lean{SpecialMatrices.d_mul_s_mul_d_inv_eq_s}
\leanok
    We have that for all $\delta \in F^\times$ and $\sigma \in F$
    \[
    d_\delta s_\sigma d^{-1}_\delta = s_{\sigma \delta^{-2}}.
    \]
\end{lemma}
\begin{proof}
\leanok
    We verify by matrix multiplication that indeed:

    \begin{equation*}
        d_\delta s_\sigma d^{-1}_\delta = \! \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \begin{bmatrix} 1 & 0 \\ \sigma & 1 \end{bmatrix} \begin{bmatrix} \delta^{-1} & 0 \\ 0 & \delta \end{bmatrix} = \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \! \begin{bmatrix} \delta^{-1} & 0 \\ \sigma \delta^{-1} & \delta \end{bmatrix} \! = \! \begin{bmatrix} 1 & 0 \\ \sigma \delta^{-2} & 1 \end{bmatrix} \! = s_{\sigma \delta^{-2}}.
    \end{equation*}
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma d_mul_s_mul_d_inv_eq_s (δ : Fˣ) (σ : F) : d δ * s σ * (d δ)⁻¹ = s (σ * δ⁻¹ * δ⁻¹) := by
    simp; ext <;> simp [s, d, mul_comm]
\end{verbatim}
\end{footnotesize}



\begin{lemma}
\label{SpecialMatrices.w_mul_d_eq_d_inv_w}
\uses{SpecialMatrices.d, SpecialMatrices.w}
\lean{SpecialMatrices.w_mul_d_eq_d_inv_w}
\leanok
For any $\delta \in F^\times$ we have:
\[ 
w d_\delta w^{-1} = d^{-1}_\delta.
\]
\end{lemma}
\begin{proof} 
\leanok
We verify by matrix multiplication that indeed
\begin{align*}
w d_\delta w^{-1} &= \begin{bmatrix} 0 & 1 \\ - 1 & 0 \end{bmatrix} \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \begin{bmatrix} 0 & - 1 \\ 1 & 0 \end{bmatrix}\\
&=  \begin{bmatrix} 0 & 1 \\ - 1 & 0 \end{bmatrix} \begin{bmatrix} 0 & - \delta \\ \delta^{-1} & 0 \end{bmatrix}\\
&= \! \begin{bmatrix} \delta^{-1} & 0 \\ 0 & \delta \end{bmatrix} \!= d^{-1}_\delta. 
\end{align*}
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma w_mul_d_eq_d_inv_w  (δ : Fˣ):  w * (d δ) = (d δ)⁻¹ * w :=  by
    rw [← mul_inv_eq_iff_eq_mul]
    exact w_mul_d_mul_inv_w_eq_inv_d _
\end{verbatim}
\end{footnotesize}

We can now express familiar kinds of matrices of $\SL_2(F)$ in terms of these three matrices:

First we note a straightforward observation:
\begin{corollary}
    \label{det_eq_mul_diag_of_lower_triangular}
    \lean{det_eq_mul_diag_of_lower_triangular}
    \leanok
    The determinant of a $2 \times 2$ lower triangular matrix, $M$, is the product of the diagonal entries $\det(M) = M_{11} M_{22}$.
\end{corollary}
\begin{proof}
\leanok
We use the $2 \times 2$ determinant formula.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma det_eq_mul_diag_of_lower_triangular (S : SL(2,F)) (hβ : S.1 0 1 = 0) :
    S 0 0 * S 1 1 = 1 := by
    have det_eq_one : det (S.val) = 1 := by simp
    simp only [det_fin_two, hβ, zero_mul, sub_zero] at det_eq_one
    exact det_eq_one
\end{verbatim}
\end{footnotesize}

\begin{corollary}
    \label{SpecialLinearGroup.fin_two_diagonal_iff}
    \lean{SpecialLinearGroup.fin_two_diagonal_iff}
    \leanok
    A $2 \times 2$ matrix of $\SL_2(F)$, $x$ is a diagonal matrix if and only if $x = d_\delta$ for some $\delta \in F^\times$.
\end{corollary}
\begin{proof}
\uses{det_eq_mul_diag_of_lower_triangular}
\leanok
    Since $x$ is diagonal and belongs to the special linear group, the determinant is $x_{11} x_{22} = 1$ which shows $x_{11} = x_{22}^{-1}$, as required.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma SpecialLinearGroup.fin_two_diagonal_iff (x : SL(2,F)) :
    x 0 1 = 0 ∧ x 1 0 = 0 ↔ ∃ δ : Fˣ, d δ = x := by
    constructor
    · rintro ⟨hβ, hγ⟩
      rcases get_entries x with ⟨α, β, γ, δ, hα, -, -, hδ, -⟩
      have det_eq_mul_diagonal := det_eq_mul_diag_of_lower_triangular x hβ
      have α_is_unit : IsUnit α := isUnit_of_mul_eq_one α δ (hα ▸ hδ ▸ det_eq_mul_diagonal)
      have δ_is_unit : IsUnit δ := isUnit_of_mul_eq_one_right α δ (hα ▸ hδ ▸ det_eq_mul_diagonal)
      have δ_ne_zero : x.1 1 1 ≠ 0 := by exact IsUnit.ne_zero <| hδ.symm ▸ δ_is_unit
      use α_is_unit.unit
      rw [mul_eq_one_iff_eq_inv₀ δ_ne_zero] at det_eq_mul_diagonal
      ext <;> simp [d, hα, hβ, hγ, det_eq_mul_diagonal]
    · rintro ⟨δ, h⟩
      rw [SpecialLinearGroup.fin_two_ext_iff] at h
      rcases h with ⟨-, h₁, h₂, -⟩
      split_ands <;> simp [d, ← h₁, ← h₂]
\end{verbatim}
\end{footnotesize}

\begin{corollary}
    \label{SpecialLinearGroup.fin_two_shear_iff}
    \uses{SpecialMatrices.s, det_eq_mul_diag_of_lower_triangular}
    \lean{SpecialLinearGroup.fin_two_shear_iff}
    \leanok
    A matrix of $\SL_2(F)$, $x$ is a shear matrix, that is of the form $\begin{bmatrix}
        \alpha & 0\\
        \sigma & \alpha
    \end{bmatrix}$ if and only if either $x = s_\sigma$ or $x = - s_\sigma$ for some $\sigma \in F$.
\end{corollary}
\begin{proof}
\leanok
Again using the formula for the determinant of a $2 \times 2$ matrix to show that indeed if $x$ is a shear matrix in the special linear group then $x_{11}^2 = x_{22}^2 = 1$ then $\alpha = \pm 1$, as required.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma SpecialLinearGroup.fin_two_shear_iff (x : SL(2,F)) :
    x 0 0 = x 1 1 ∧ x 0 1 = 0 ↔ (∃ σ, s σ = x) ∨ ∃ σ, -s σ = x := by
    constructor
    · rintro ⟨α_eq_δ, β_eq_zero⟩
      have α_eq_one_or_neg_one := α_eq_δ.symm ▸ det_eq_mul_diag_of_lower_triangular x β_eq_zero
      rw [← sq, sq_eq_one_iff] at α_eq_one_or_neg_one
      rcases α_eq_one_or_neg_one with (α_eq_one | α_eq_neg_one)
      · left
        use x.1 1 0
        ext <;> simp [s, α_eq_one, β_eq_zero, α_eq_δ ▸ α_eq_one]
      · right
        use - x.1 1 0
        ext <;> simp [s, α_eq_neg_one, β_eq_zero, α_eq_δ ▸ α_eq_neg_one]
    · rintro (⟨σ,h⟩ | ⟨σ, h⟩)
      repeat' rw [SpecialLinearGroup.fin_two_ext_iff] at h; rcases h with ⟨hα, hβ, -, hδ⟩
      · simp [← hα, ← hδ, ← hβ, s]
      · simp [← hα, ← hδ, ← hβ, s]
\end{verbatim}
\end{footnotesize}

\begin{corollary}
    \label{SpecialLinearGroup.fin_two_antidiagonal_iff}
    \lean{SpecialLinearGroup.fin_two_antidiagonal_iff}
    \leanok
    A matrix $A \in \SL_2(F)$ is anti-diagonal, that is of the from $\begin{bmatrix}
        0 & \beta\\
        \gamma & 0
    \end{bmatrix}$ if and only if $A = d_\delta w$
\end{corollary}
\begin{proof}
\leanok
This is shown by direct computation, we observe that $w$ flips the rows and changes the sign of one the flipped rows to account for the determinant needing to be equal to one.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma SpecialLinearGroup.fin_two_antidiagonal_iff (x : SL(2,F)) :
    x 0 0 = 0 ∧ x 1 1 = 0 ↔ ∃ δ : Fˣ, (d δ) * w = x := by
    constructor
    · rintro ⟨hα, hδ⟩
      have det_eq_one : det (x : Matrix (Fin 2) (Fin 2) F) = 1 := by rw [SpecialLinearGroup.det_coe]
      rw [det_fin_two, hα, hδ, zero_mul, zero_sub, ← neg_mul, neg_mul_comm] at det_eq_one
      have β_is_unit : IsUnit (x 0 1) := by exact isUnit_of_mul_eq_one (x 0 1) (-x 1 0) det_eq_one
      rw [← neg_mul_comm] at det_eq_one
      have neg_β_inv_eq : -(x 0 1)⁻¹ = x 1 0 := by
        rw [neg_inv]
        refine inv_eq_of_mul_eq_one_right det_eq_one
      use β_is_unit.unit
      ext <;>
      simp [d, w, hα, hδ, neg_β_inv_eq]
    · rintro ⟨δ, rfl⟩
      split_ands  <;>
      simp [d, w]
\end{verbatim}
\end{footnotesize}

From these relations we can now single out the following subgroups of $\SL_2(F)$.

\subsubsection{Special subgroups of $\SL_2(F)$}

\begin{definition}[The subgroup of diagonal matrices]
\label{SpecialSubgroups.D}
\lean{SpecialSubgroups.D}
\leanok
    The set of diagonal matrices with matrix multiplication is a subgroup of $\SL_2(F)$: 
    \[
    D = \{d_\delta \; | \; \delta \in F^\times \} = \left\{ \begin{bmatrix}\delta & 0\\ 0 & \delta^{-1}\end{bmatrix} \; | \; \delta \in F^\times \right\}
    \]
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def D (F : Type*) [Field F] : Subgroup SL(2,F) where
    carrier := { d δ | δ : Fˣ }
    mul_mem' := by
                intro S Q hS hQ
                simp at *
                obtain ⟨δS, hδS⟩ := hS
                obtain ⟨δQ, hδQ⟩ := hQ
                use δS * δQ
                rw [← hδS, ← hδQ]
                simp
    one_mem' := ⟨1, by simp⟩
    inv_mem' := by
                intro S
                simp
                intro δ hS
                use δ⁻¹
                simp [← hS]
\end{verbatim}
\end{footnotesize}

\begin{definition}[The subgroup of shear matrices]
\label{SpecialSubgroups.S}
\lean{SpecialSubgroups.S}
\leanok
    The set of shear matrices with matrix multiplication is a subgroup of $\SL_2(F)$:
    \[
    S = \{s_\sigma \; | \sigma \in F\} = \left\{\begin{bmatrix}1 & 0\\ \sigma & 1\end{bmatrix} \; | \; \sigma \in F \right\}
    \]
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def S (F : Type*) [Field F] : Subgroup SL(2,F) where
    carrier := { s σ | σ : F }
    mul_mem' := by
                intro S Q hS hQ
                simp at *
                obtain ⟨σS, hσS⟩ := hS
                obtain ⟨σQ, hσQ⟩ := hQ
                use σS + σQ
                rw [← hσS, ← hσQ]
                simp
    one_mem' := ⟨0, by simp⟩
    inv_mem' := by
                intro S hS
                simp at *
                obtain ⟨σ, hσ⟩ := hS
                use (-σ)
                simp [← hσ]
\end{verbatim}
\end{footnotesize}

\begin{definition}[The subgroup of lower triangular matrices]
\label{SpecialSubgroups.L}
\lean{SpecialSubgroups.L}
\leanok
    The set of lower triangular matrices (see below) with matrix multiplication is a subgroup of $\SL_2(F)$
    \[
    L = DS
    \]
    where $DS = \{d_\delta s_\sigma \; | \; \delta \in F^\times \text{ and } \sigma \in F \}$ is the pointwise product of $D$ and $S$.
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def L (F : Type*) [Field F] : Subgroup SL(2,F) where
    carrier := { d δ * s σ | (δ : Fˣ) (σ : F) }
    mul_mem' := by
                rintro A₁ A₂ ⟨δ₁, σ₁, h₁⟩ ⟨δ₂, σ₂, h₂⟩
                use (δ₁ * δ₂), (σ₁*δ₂*δ₂ + σ₂)
                rw [← h₁, ← h₂]
                ext
                · simp [d, s]
                · simp [d, s]
                · field_simp [d, s]; ring
                · simp [d, s, mul_comm]
    one_mem' := ⟨1, 0, by simp⟩
    inv_mem' := by
                rintro A ⟨δ, σ, h⟩
                use δ⁻¹, -σ * δ⁻¹ * δ⁻¹
                rw [← h]
                simp [d_mul_s_eq_ds, Matrix.SpecialLinearGroup.SL2_inv_expl]
                ext <;> simp [ds]
\end{verbatim}
\end{footnotesize}

\begin{definition}[The subgroup of containing diagonal and antidigonal matrices]
    \label{SpecialSubgroups.DW}
    \lean{SpecialSubgroups.DW}
    \leanok
    The set of all diagonal and anti-diagonal matrices with matrix multiplication is a subgroup of $\SL_2(F)$

    \begin{equation}\label{antidiag} DW = \langle D, w\rangle  = \{d_\delta \} \cup \{ d_\delta w \} 
        % =  \left\{  \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \begin{bmatrix} 0 & 1 \\ -1 & 0 \end{bmatrix} \right\} 
        % = \left\{ \begin{bmatrix} 0 & \delta \\ -\delta^{-1} & 0 \end{bmatrix}  \right\}. 
    \end{equation}
\end{definition}
\begin{footnotesize}
\begin{verbatim}
    def DW (F : Type*) [Field F] : Subgroup SL(2,F) where
    carrier := { d δ | δ : Fˣ} ∪ { d δ * w | δ : Fˣ}
    mul_mem' := by
      rintro x y (⟨δ₁, rfl⟩ | ⟨δ₁, rfl⟩) (⟨δ₂, rfl⟩ | ⟨δ₂, rfl⟩)
      · rw [d_mul_d_eq_d_mul]
        left
        use δ₁ * δ₂
      · rw [← mul_assoc, d_mul_d_eq_d_mul]
        right
        use δ₁ * δ₂
      · rw [mul_assoc, w_mul_d_eq_d_inv_w, inv_d_eq_d_inv, ← mul_assoc, d_mul_d_eq_d_mul]
        right
        use δ₁ * δ₂⁻¹
      · rw [mul_assoc, ← mul_assoc w, w_mul_d_eq_d_inv_w, mul_assoc _ w, w_mul_w_eq_neg_one,
         inv_d_eq_d_inv, ← mul_assoc, d_mul_d_eq_d_mul, mul_neg_one, neg_d_eq_d_neg]
        left
        use -(δ₁ * δ₂⁻¹)
    one_mem' := by left; rw [← d_one_eq_one]; use 1
    inv_mem' := by
      intro x h
      simp at h
      rcases h with (⟨δ, rfl⟩ | ⟨δ, rfl⟩)
      · simp
      · simp
\end{verbatim}
\end{footnotesize}

\begin{remark}
    It is possible to have specify the subgroup $DW$ in \ref{SpecialSubgroups.DW} as the subgroup closure of $D \sqcup \langle w \rangle$
    but then it would require some additional work to show that the underlying set is indeed $D \cup Dw$.
\end{remark}


\begin{corollary}
    \label{mem_L_iff_lower_triangular}
    \uses{SpecialMatrices.d, SpecialMatrices.s}
    \lean{mem_L_iff_lower_triangular}
    \leanok
    The subgroup $L \le \SL_2(F)$ is the subgroup of $2 \times 2$ lower triangular matrices with determinant one, $L =\left\{\begin{bmatrix}
    \alpha & 0\\
    \gamma & \delta
    \end{bmatrix} \; | \; \alpha, \gamma, \delta \in F \text{ and } \alpha \delta = 1 \right\}$.
\end{corollary}
\begin{proof}
\leanok
    Observe that for every $l \in L$ there is some $\delta \in F^\times$ and $\sigma \in F$ such that $l  = d_\delta s_\sigma = \begin{bmatrix}
        \delta & 0\\
        \sigma * \delta^{-1} & \delta^{-1}
    \end{bmatrix}$ which is lower triangular. 
    
    Furthermore, for every lower triangular matrix $L = \begin{bmatrix}
        \alpha & 0\\
        \gamma & \delta
    \end{bmatrix}$ 
    
    Setting $\delta = \alpha \in F^\times$ as $\alpha \delta = 1$ and setting $\sigma = \gamma \alpha$

    indeed yields the equality

    \[
    d_\delta s_\sigma = \begin{bmatrix}
        \alpha & 0\\
        \gamma & \delta
    \end{bmatrix}
    \]

    Thus $L = D S$ is the set of lower triangular matrices.
\end{proof}
\begin{footnotesize}
    \begin{verbatim}
    lemma mem_L_iff_lower_triangular [DecidableEq F] {x : SL(2,F)} :
        x ∈ L F ↔ ∃ a c d, !![a, 0; c, d] = (x : Matrix (Fin 2) (Fin 2) F) := by
        constructor
        · intro hx
          obtain ⟨δ, σ, h⟩ := hx
          use δ, σ * δ⁻¹, δ⁻¹
          rw [← h]
          ext <;> simp [d, s, mul_comm]
        · rintro ⟨a, c, d, hx⟩
          have had : det (x : Matrix (Fin 2) (Fin 2) F) = 1 := by simp
          simp [← hx] at had
          have a_is_unit : IsUnit a := isUnit_of_mul_eq_one a d had
          have a_inv_eq_d : a⁻¹ = d := DivisionMonoid.inv_eq_of_mul a d had
          use a_is_unit.unit, c * a_is_unit.unit
          simp [SpecialMatrices.d, SpecialMatrices.s, lower_triangular, had]
          ext <;> field_simp [a_inv_eq_d, had, ← hx]; exact (eq_one_div_of_mul_eq_one_right had).symm
    \end{verbatim}
    \end{footnotesize}

\begin{remark}
    To define the subgroups $D$, $S$ and $L$ in Lean. 
    
    One has to:
    
    \begin{enumerate}
        \item Specify what the underlying set is, what is called the \texttt{carrier}.
        \item Prove that the set is closed under multiplication, that is, provide a proof term to the field \texttt{mul\textunderscore mem'}.
        \item Prove that the set contains the identity element of the group, that is, provide a proof term to the field \texttt{one\textunderscore mem'}.
        \item Show that the group is closed under the inversion operator $(-)^{-1}$, \texttt{inv\textunderscore mem'}.
    \end{enumerate}

    Once these four fields have been filled in, one has succesfully defined a subgroup in Lean.
\end{remark}


\begin{remark}
    Despite the definition of $L$ as being $D S$, some work has to be shown that indeed $DS = D \sqcup S$.
    
    If either $D$ or $S$ were normal in $\SL_2(F)$, this fact would be immediate as we would be able to use \texttt{mul\_normal} or \texttt{normal\_mul}:
    
    % \begin{verbatim}
    %     ∀ {G : Type u_2} [inst : Group G] (N H : Subgroup G) [inst_1 : N.Normal], ↑(N ⊔ H) = ↑N * ↑H
    % \end{verbatim}

    However, given neither $D$ or $S$ are normal in $\SL_2(F)$ slightly more work is needed to show this.
    
    It is interesting how Lean really forces either increased understanding or increased frustration.
\end{remark}

These elements and subgroups are fundamental to this paper and this notation will be used throughout.

\begin{definition}[$(D, \cdot) \cong (F^\times, \cdot)$]
\label{SpecialSubgroups.D_iso_units}
\uses{SpecialSubgroups.D, SpecialMatrices.d_mul_d_eq_d_mul}
\lean{SpecialSubgroups.D_iso_units}
\leanok
The map $\psi : F^\times \overset{\sim}{\rightarrow} D$ defined by $\delta \mapsto d_\delta$ defines a group isomorphism.
\end{definition}

\begin{proof}
    \leanok
    The function $\psi: F^\times \rightarrow D$ defined by $\psi(\delta) = d_\delta$ is a homomorphism between the group $F^\times$ under normal multiplication and $D$ under normal matrix multiplication:
\begin{align*} \psi(\delta \rho) = d_{\delta \rho} =  d_\delta d_\rho = \psi(\delta) \psi(\rho). \tag{by Lemma \ref{6.1}}
\end{align*}
Observe that $\psi$ is trivially injective and surjective and thus an isomorphism. So $D\cong F^\times$ and $D$ is a subgroup of $L$.\\
\end{proof}
\begin{footnotesize}
\begin{verbatim}
def D_iso_units (F : Type*) [Field F] : SpecialSubgroups.D F ≃* Fˣ where
    toFun d := ⟨
                d.val 0 0,
                d.val 1 1,
                by obtain ⟨δ, hδ⟩ := d.property; rw [← hδ]; simp [SpecialMatrices.d],
                by obtain ⟨δ, hδ⟩ := d.property; rw [← hδ]; simp [SpecialMatrices.d]
                ⟩
    invFun δ := ⟨d δ, by use δ⟩
    left_inv A := by
                  obtain ⟨δ, hδ⟩ := A.property
                  rw [← Subtype.coe_inj, ← hδ]
                  ext <;> simp [SpecialMatrices.d, ← hδ]
    right_inv a := by ext; rfl
    map_mul' X Y := by
                  obtain ⟨δ₁, hδ₁⟩ := X.property
                  obtain ⟨δ₂, hδ₂⟩ := Y.property
                  simp [Subgroup.coe_mul, Fin.isValue, SpecialLinearGroup.coe_mul]
                  congr
                  repeat'
                    simp_rw [← hδ₁, ← hδ₂]
                    simp [SpecialMatrices.d, mul_comm]
\end{verbatim}
\end{footnotesize}



\begin{definition}[ $(S, \cdot) \cong (F, +)$ ]
\label{SpecialSubgroups.S_iso_F}
\uses{SpecialSubgroups.S}
\lean{SpecialSubgroups.S_iso_F}
\leanok
    The map $\phi : F \overset{\sim}{\rightarrow} S$ defined by $\sigma \mapsto s_\sigma$ defines a group isomorphism.
\end{definition}

\begin{proof}
\uses{SpecialMatrices.s_mul_s_eq_s_add}
\leanok
     The function $\phi: F \rightarrow T$ defined by $\phi(\sigma) = s_\sigma$ is a homomorphism between the group $F$ under addition and $S$ under normal matrix multiplication:
\begin{align*} \phi(\sigma + \gamma) = s_{\sigma + \gamma} = s_\sigma s_\gamma = \phi(\sigma) \phi(\gamma). \tag{by Lemma \ref{6.1}}
\end{align*}
It is clear that $\phi$ is injective and surjective and thus an isomorphism. So $ S \cong F$ and $S$ is a subgroup of $L$. \\
\end{proof}
\begin{footnotesize}
\begin{verbatim}
def S_iso_F (F : Type*) [Field F]: S F ≃* (Multiplicative F) where
    toFun T := T.val 1 0
    invFun σ := ⟨s σ, by use σ⟩
    left_inv T := by
      obtain ⟨σ, hσ⟩ := T.property
      rw [← Subtype.coe_inj, ← hσ]
      ext <;> simp [s, ← hσ]
    right_inv σ := by simp [s]
    map_mul' X Y := by
      obtain ⟨σ₁, hσ₁⟩ := X.property
      obtain ⟨σ₂, hσ₂⟩ := Y.property
      simp only [Subgroup.coe_mul, Fin.isValue, SpecialLinearGroup.coe_mul]
      simp_rw [← hσ₁, ← hσ₂]
      simp [s]
      rfl
\end{verbatim}
\end{footnotesize}

\begin{remark}[Multiplicative]
    Putting the keyword \texttt{Multiplicative} in front a structure which carries an additive structure
    it creates a copy of the additive structure and carries it over to be defined as a multiplicative structure
    on the type. 
\end{remark}


\begin{lemma}
\label{SpecialSubgroups.normal_S_subgroupOf_L}
\lean{SpecialSubgroups.normal_S_subgroupOf_L}
\leanok
$S$ is a normal subgroup of $L$
\end{lemma}
\begin{proof}
    \leanok
    Let $s_\gamma$ and $d_\delta s_\sigma$ be arbitrary elements of $T$ and $H$ respectively. Conjugating $s_\gamma$ by $d_\delta s_\sigma$ gives,
\begin{align*} (d_\delta s_\sigma) s_\gamma (d_\delta s_\sigma)^{-1} &= (d_\delta s_\sigma) s_\gamma (t^{-1}_\sigma d^{-1}_\delta) \\[1.5ex]
&=
d_\delta (s_\sigma s_\gamma s_{-\sigma}) d^{-1}_\delta \qquad \tag{since $t^{-1}_\sigma=s_{-\sigma}$} \\[1.5ex] 
&=
d_\delta s_\gamma d^{-1}_\delta \tag{by Lemma \ref{6.1}} \\[1.5ex] 
&= s_{\gamma \delta^{-2}} \in S. \tag{by Lemma \ref{6.1}}
\end{align*}
Since $s_\gamma$ was chosen arbitrarily from $\SL_2(F)$we have ($d_\delta s_\sigma) S (d_\delta s_\sigma)^{-1} = S$ and since $d_\delta s_\sigma$ was chosen arbitrarily from $L$, we have that $S \vartriangleleft L$. \\
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma normal_S_subgroupOf_L {F : Type*} [Field F] : ((S F).subgroupOf (L F)).Normal := by
    rw [← normalizer_eq_top_iff]
    ext x
    constructor
    · intro _hx
      exact mem_top _
    · intro hx
      rw [← @subgroupOf_self] at hx
      rw [@mem_subgroupOf] at hx
      obtain ⟨δ, σ, hx⟩ := hx
      rw [@mem_normalizer_iff'']
      intro t'
      constructor
      · rintro ⟨σ', hσ'⟩
        rw [mem_subgroupOf]
        dsimp at hσ' ⊢
        rw [← hx, ← hσ', _root_.mul_inv_rev, s_inv,
          inv_d_eq_d_inv, mul_assoc, mul_assoc (s (-σ)), ← mul_assoc (s σ'),
          ← mul_assoc (d δ⁻¹), ← mul_assoc (d δ⁻¹), d_eq_inv_d_inv δ,
          d_mul_s_mul_d_inv_eq_s, s_mul_s_eq_s_add, s_mul_s_eq_s_add]
        rw [S, inv_inv, neg_add_cancel_comm_assoc, mem_mk, Set.mem_setOf_eq]
        use σ' * (δ : F) * (δ : F)
      · rintro ⟨σ', hσ'⟩
        rw [mem_subgroupOf]
        dsimp at hσ' ⊢
        have hσ : (t' : SL(2,F)) = (x : SL(2,F)) * s σ' * (x : SL(2,F))⁻¹ := by rw [hσ']; group
        rw [hσ, ← hx]
        rw [_root_.mul_inv_rev, s_inv, inv_d_eq_d_inv, mul_assoc (d δ), s_mul_s_eq_s_add,
           mul_assoc (d δ), ← mul_assoc (s (σ + σ')), s_mul_s_eq_s_add, ← mul_assoc,
           ← inv_d_eq_d_inv, d_mul_s_mul_d_inv_eq_s, add_neg_cancel_comm, Units.val_inv_eq_inv_val]
        use σ' * (δ : F)⁻¹ * (δ :F)⁻¹
\end{verbatim}
\end{footnotesize}

\begin{remark}[Subgroups of subgroups in Lean]
    \label{lattice}
    In Lean, for this particular scenario, $S$ is considered to be a subgroup of $\SL_2(F)$. 
    
    It is fairly easy to see that $S \not\lhd \SL_2(F)$. When we say that $S \lhd L$, we are implicitly restricting $S$ to be a subset of $L$ and thus we are actually thinking about the subgroup $S \cap L$,
    but in fact this does not change anything because $S = S \sqcap L$ as $S \le \SL_2(F)$.

    Informally we do not think twice about this, but when formalizing this we do need to be clear which is the ambient group for $S$ to be normal and in this case it should be a subgroup of $L$, rather than $\SL_2(F)$.
    
    So this is why the informal statement corresponds to the formal statement:

    \begin{footnotesize}
    \begin{verbatim}
    Normal (S F).subgroupOf (L F) 
    \end{verbatim}
    \end{footnotesize}

    This example highlights how as useful as it is that Lean keeps track of what the ambient groups are, it is often the case that we look at the same object under a different lense such as in this case, where we restrict it to be a 
    subgroup of another group that contains it. It turns out that there is learning curve to becoming comfortable with these transitions.
    
    On the positive side, the automation leans offers, that is, the tactics and the unification algorithm (the algorithm which allows you to substitute equal terms when say you use the  \texttt{rw} tactic) is continually being refined, 
    and it is increasingly able to do a lot of coercions on its own.
\end{remark}


\begin{lemma}
\label{SpecialSubgroups.D_join_S_quot_S_subgroupOf_D_join_S_mulEquiv_D_subgroupOf_D_join_S}
\uses{SpecialSubgroups.D, SpecialSubgroups.S}
\lean{SpecialSubgroups.D_join_S_quot_S_subgroupOf_D_join_S_mulEquiv_D_subgroupOf_D_join_S}
\leanok
    $L / S \cong D$.
\end{lemma}
\begin{proof} 
\uses{SpecialSubgroups.normal_S_subgroupOf_L}
\leanok
The function $\pi: L \rightarrow D$ defined by $\pi(d_\delta s_\sigma) = d_\delta$ is a homomorphism between $L$ under normal matrix multiplication and $D$ under normal matrix multiplication:
\begin{align*} \pi(d_\delta s_\sigma d_\rho s_\gamma) &= \pi(d_\delta d_\rho s_\sigma s_\gamma) \tag{where $\sigma = \sigma \rho^{2}$ by Lemma \ref{6.1}}
\\ &= d_\delta d_\rho
\\ &= \pi(d_\delta s_\sigma)\pi(d_\rho s_\gamma).
\end{align*}

We see that $\pi$ is trivially surjective and has kernel
\begin{align*}  \ker(\pi) &= \{ d_\delta s_\sigma \in L : \pi(d_\delta s_\sigma) = I_{\SL_2(F)}\} = S.
\end{align*}
Thus by the First Isomorphism Theorem,
\begin{align*} L / \ker(\pi) &\cong \text{Im}(\pi), \\
L / &\cong D.
\end{align*}
\end{proof}
\begin{footnotesize}
\begin{verbatim}
noncomputable def D_join_S_quot_S_subgroupOf_D_join_S_mulEquiv_D_subgroupOf_D_join_S
    (F : Type*) [Field F] :=
    (QuotientGroup.quotientInfEquivProdNormalQuotient
      (H := (D F).subgroupOf (D F ⊔ S F:)) (N := (S F).subgroupOf (D F ⊔ S F :))).symm
\end{verbatim}
\end{footnotesize}


\begin{remark}
Interestingly, this proof was quite hard to formalise for reasons I will expand on below, but first let me introduce some ideas.

There are two complete lattice structures at play here: one where the top element is $\top = \SL_2(F)$ and another lattice which is the corresponding sublattice where $\top = D \sqcup S$.
The second sublattice is crucial because we need $S$ to be normal in an ambient group, and clearly $S \not\lhd \SL_2(F)$; therefore when restricting $S$ to begin a subgroup of $D \sqcup S = L$. Given $S$ is a subgroup of $D \sqcup S = L$ as $S \le S\sqcup D = L$, considered as a subgroup of $D \sqcup S = L$ we as shown in \ref{SpecialSubgroups.normal_S_subgroupOf_L}
it is normal $S$ a subgroup of $L$.

This eventually entails to using the theorem called 
\texttt{QuotientGroup.quotientInfEquivProdNormalQuotient} which corresponds to the statement:

\begin{footnotesize}
\begin{verbatim}
    H ⧸ N.subgroupOf H ≃* (H ⊔ N) ⧸ N.subgroupOf (H ⊔ N)
\end{verbatim}
\end{footnotesize}


Which is in fact the second isomorphism theorem, not the first isomorphism theorem! Which contrasts to how the statement was proved informally.

where in for this particular theorem, \texttt{H} is specialized to:

\begin{footnotesize}
\begin{verbatim}
    H := (D F).subgroupOf (D F ⊔ S F)
\end{verbatim}
\end{footnotesize}


and  \texttt{N} is specialized to:

\begin{footnotesize}
\begin{verbatim}
    N := (S F).subgroupOf (D F ⊔ S F)
\end{verbatim}
\end{footnotesize}


recall that within Lean, \texttt{F} denotes the base field for $\SL_2(F)$, $D$ and $S$.

Written informally, it corresponds to

\[
D \cong \frac{D}{\bot} = \frac{D}{S \sqcap D} \cong \frac{D \sqcup S}{S} = \frac{L}{S}
\]


\end{remark}

\section{The Center of $\SL_2(F)$}

\begin{definition}
% \lean{Subgroup.center}
% \leanok
The \textbf{centre} $Z(G)$ of a group $G$ is the set of elements of  $G$ that commute with every element of $G$.
\begin {equation*} Z(G) = \{ z \in G : \forall g \in G, \hspace{6pt} gz=zg \}. \end{equation*}
It is an immediate observation that $Z(G)$ is a normal subgroup of $G$, 
since for each $z \in Z$, $gzg^{-1} = gg^{-1}z = z$, $\forall g \in G$. It's also clear that a group is abelian if and only if $Z(G)=G$.
\end{definition}

\begin{definition}
\label{SpecialSubgroups.Z}
\lean{SpecialSubgroups.Z}
\leanok
    Let $R$ be a commutative ring and define $Z$ to be the subgroup generated by $- I \in \SL_2(R)$
\end{definition}

\begin{remark}[Z as the subgroup closure of $\{-I\}$]
    Observer that the subgroup generated by an element $g \in G$, $\langle g \rangle$, can be thought of more generally within any lattice as the closure of a singleton set ${g}$. Therefore, the subgroup generated by $-I$ is equal to
    
    \[\langle -I \rangle = \overline{\{-1\}} = \inf \{ K \le G \; | \; \{-1\} \subseteq K \}.\]

    When taking the closure of a singleton, when the ambient lattice is the subgroup lattice; and the closure corresponds to taking the powers of the element in the singleton $\{g\}$,
    which is what is typically understood as the subgroup generated by $g$.

    The way $Z$ is defined in Lean is thus:

    \begin{footnotesize} 
    \begin{verbatim}
        def Z (R : Type*) [CommRing R] : Subgroup SL(2,R) := 
            closure {(-1 : SL(2,R))}
    \end{verbatim}
    \end{footnotesize}

\end{remark}

\begin{corollary}
\label{SpecialSubgroups.closure_neg_one_eq}
\lean{SpecialSubgroups.closure_neg_one_eq}
\leanok
The subgroup closure of the singleton $\{-I\}$, or equivalently, the subgroup generated by $-I$ equals $\overline{\{-I\}} = \{I, -I\}$
\end{corollary}
\begin{proof}
\leanok
Since $-1^2 = 1$, we have that $-I^2 = I$ and thus the result follows.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma closure_neg_one_eq : (closure {(-1 : SL(2,R))} : Set SL(2,R)) = {1, -1} := by
    ext x
    constructor
    · intro hx
      rw [← zpowers_eq_closure, SetLike.mem_coe, mem_zpowers_iff] at hx
      obtain ⟨k, rfl⟩ := hx
      rw [Set.mem_insert_iff, Set.mem_singleton_iff]
      by_cases hk : Even k
      · left
        apply Even.neg_one_zpow hk
      · right;
        rw [Int.not_even_iff_odd] at hk
        exact Odd.neg_one_zpow hk
    · intro hx
      rw [Set.mem_insert_iff, Set.mem_singleton_iff] at hx
      rcases hx with (rfl | rfl)
      · rw [SetLike.mem_coe, mem_closure_singleton]
        use 2
        apply Even.neg_one_zpow (by norm_num)
      · rw [SetLike.mem_coe]
        exact mem_closure_singleton_self (-1)
\end{verbatim}
\end{footnotesize}


\begin{lemma}
\label{SpecialSubgroups.center_SL2_eq_Z}
\uses{SpecialSubgroups.Z}% Matrix.SpecialLinearGroup.mem_center_iff}
\lean{SpecialSubgroups.center_SL2_eq_Z}
\leanok
$Z(\SL_2(F)) = \langle - I_{\SL_2(F)}\rangle = Z$.
\end{lemma}
\begin{proof} 
\leanok
    Take an arbitrary element $x=\begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \in \SL_2(F)$and  an arbitrary element $z = \begin{bmatrix} z_1 & z_2 \\ z_3 & z_4 \end{bmatrix} \in Z$ and consider their product:

\begin{align}\label{myeq1} zx = \begin{bmatrix} z_1 & z_2 \\ z_3 & z_4 \end{bmatrix} \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} &= \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} z_1 & z_2 \\ z_3 & z_4 \end{bmatrix} = xz, \nonumber \\[1.5ex]
\begin{bmatrix} z_1 \alpha + z_2 \gamma & z_1 \beta + z_2 \delta \\ z_3 \alpha + z_4 \gamma & z_3 \beta + z_4 \delta \end{bmatrix} &= \begin{bmatrix} z_1 \alpha + z_3 \beta & z_2 \alpha + z_4 \beta \\ z_1 \gamma + z_3 \delta & z_2 \gamma + z_4 \delta \end{bmatrix}.
\end{align}

\noindent Equating either the top left or bottom right entries, we see that $z_2 \gamma = z_3 \beta$. Since $\beta$ and $\gamma$ can take any values in $F$, for equality to always hold we must have $z_2 = 0 = z_3$. Hence equation (\ref{myeq1}) simplifies to

\begin{equation*} \begin{bmatrix} z_1 \alpha & z_1 \beta \\ z_4 \gamma & z_4 \delta \end{bmatrix} = \begin{bmatrix} z_1 \alpha & z_4 \beta \\ z_1 \gamma & z_4 \delta \end{bmatrix}. \end{equation*}

Thus 
\begin{equation*} 
    z_1 = z_4 \qquad  \text{and} \qquad z =  
    \begin{bmatrix} z_1 & 0 \\ 0 & z_1 \end{bmatrix}. 
\end{equation*}
Since we are working in the special linear group, det$(z)=1$, thus $z_1 = \pm 1$ and $Z = \langle - I_{\SL_2(F)}\rangle$ as required. Observe that this is a cyclic group of order 2 except in the case of $p = 2$ where $- I_{\SL_2(F)} = I_{\SL_2(F)}$. \\
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma center_SL2_eq_Z (R : Type*)  [CommRing R] [NoZeroDivisors R]: center SL(2,R) = Z R := by
    ext x
    constructor
    · intro hx
      rw [SpecialLinearGroup.mem_center_iff] at hx
      obtain ⟨z, z_pow_two_eq_one, hz⟩ := hx
      simp at z_pow_two_eq_one hz ⊢
      rcases z_pow_two_eq_one with (rfl | rfl)
      · left
        ext <;> simp [← hz]
      · right
        ext <;> simp [← hz]
    · simp
      rintro (rfl | rfl) <;> simp [mem_center_iff]
\end{verbatim}
\end{footnotesize}

Following this result, for ease of notation, $Z(\SL_2(F))$ will be denoted simply by $Z$ throughout the rest of this paper.

\begin{lemma}
\label{SpecialSubgroups.exists_unique_orderOf_eq_two}
\lean{SpecialSubgroups.exists_unique_orderOf_eq_two}
\leanok
    If $p\neq 2$, then $\SL_2(F)$ contains a unique element of order 2. \\
\end{lemma}
\begin{proof}
\leanok
Consider an arbitrary element $x \in \SL_2(F)$with order 2. That is $x^2 = I_{\SL_2(F)}$, $x \neq I_{\SL_2(F)}$and thus $x=x^{-1}$.
\begin{equation*} 
    x = \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} = \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix}^{-1} = \begin{bmatrix} \delta & - \beta \\ - \gamma & \alpha \end{bmatrix}.
\end{equation*}
\noindent Thus $\alpha = \delta$, $\beta = - \beta \Rightarrow 2\beta = 0$ and $\gamma = - \gamma \Rightarrow 2\gamma = 0$. In the case of $p \neq 2$ this gives $\beta = 0 = \gamma$. So
\begin{equation*} 
    x = \begin{bmatrix} \alpha & 0 \\ 0 & \alpha \end{bmatrix}.
\end{equation*}
\noindent Also $\alpha^2 = 1$ since $x \in$ $\SL_2(F)$, so $\alpha = \pm 1$. For $x$ to have order 2, we must have $\alpha = - 1$. Hence there is a unique element of order 2, namely $- I_{\SL_2(F)}$.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma exists_unique_orderOf_eq_two [NeZero (2 : F)] : ∃! x : SL(2,F), orderOf x = 2 := by
    use -1
    split_ands
    · exact orderOf_neg_one_eq_two
    -- Now we show it is the unique element of order two
    intro x hx
    rcases get_entries x with ⟨α, β, γ, _δ, _x_eq⟩
    simp [propext (orderOf_eq_iff (Nat.le.step Nat.le.refl))] at hx
    obtain ⟨hx₁, hx₂⟩ := hx
    rw [sq, mul_eq_one_iff_eq_inv'] at hx₁
    rw [SpecialLinearGroup.fin_two_ext_iff] at hx₁
    simp [adjugate_fin_two] at hx₁
    obtain ⟨α_eq_δ, β_eq_neg_β, γ_eq_neg_γ, -⟩ := hx₁
    rw [eq_neg_iff_add_eq_zero, ← two_mul] at β_eq_neg_β γ_eq_neg_γ
    have β_eq_zero : x.1 0 1 = 0 := eq_zero_of_ne_zero_of_mul_left_eq_zero two_ne_zero β_eq_neg_β
    have γ_eq_zero : x.1 1 0 = 0 := eq_zero_of_ne_zero_of_mul_left_eq_zero two_ne_zero γ_eq_neg_γ
    have det_x_eq_one : det (x : Matrix (Fin 2) (Fin 2) F) = 1 :=  by simp
    rw [det_fin_two, β_eq_zero, zero_mul, sub_zero, α_eq_δ, mul_self_eq_one_iff] at det_x_eq_one
    rcases det_x_eq_one with (δ_eq_one | δ_eq_neg_one )
    have α_eq_δ := α_eq_δ
    · rw [δ_eq_one] at α_eq_δ
      have x_eq_one : x = 1 := by ext <;> simp [α_eq_δ, β_eq_zero, γ_eq_zero, δ_eq_one]
      specialize hx₂ 1 (by norm_num) (by norm_num)
      rw [pow_one] at hx₂
      contradiction
    · rw [δ_eq_neg_one] at α_eq_δ
      ext <;> simp [α_eq_δ, β_eq_zero, γ_eq_zero, δ_eq_neg_one]
\end{verbatim}
\end{footnotesize}

\begin{lemma}
    \label{SpecialSubgroups.card_Z_eq_two_of_two_ne_zero}
    \lean{SpecialSubgroups.card_Z_eq_two_of_two_ne_zero}
    \leanok
    If the characteristic $\textrm{char}(F) \ne 2$ then $|Z| = 2$.
\end{lemma}
\begin{proof}
\leanok
    If $\textrm{char}(F) \ne 2$ then $1 \ne -1$ as $2 \ne 0$ therefore, $I \ne -I$ which shows that $Z = \{I , -I\}$ contains two distinct elements.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma card_Z_eq_two_of_two_ne_zero [NeZero (2 : F)]: Nat.card (Z F) = 2 := by
    rw [Nat.card_eq_two_iff]
    use 1, ⟨-1, neg_one_mem_Z⟩
    split_ands
    · intro h
      rw [Subtype.ext_val_iff] at h
      -- -1 ≠ 1 for characteristic different to 2
      simp at h
    · rw [Set.eq_univ_iff_forall]
      rintro ⟨z, hz⟩
      simp at hz
      rcases hz with (rfl | rfl) <;> simp
\end{verbatim}
\end{footnotesize}

\begin{lemma}
    \label{SpecialSubgroups.card_Z_eq_one_of_two_eq_zero}
    \lean{SpecialSubgroups.card_Z_eq_one_of_two_eq_zero}
    \leanok
    If the characteristic $\textrm{char}(F) = 2$ then $|Z| = 1$. 
\end{lemma}
\begin{proof}
\leanok
    If $\textrm{char}(F) = 2$ then $1 = -1$ as $2 = 0$ therefore, $I = -I$ which shows that $Z = \{I , -I\} = \{I\}$ only contains one element.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma card_Z_eq_one_of_two_eq_zero (two_eq_zero : (2 : F) = 0) : Nat.card (Z F) = 1 := by
    rw [card_eq_one]
    ext x
    simp [(SpecialLinearGroup.neg_one_eq_one_of_two_eq_zero two_eq_zero).symm]
\end{verbatim}
\end{footnotesize}

\begin{lemma}[$Z$ is cyclic]
    \label{SpecialSubgroups.IsCyclic_Z}
    \lean{SpecialSubgroups.IsCyclic_Z}
    \leanok
\end{lemma}
\begin{proof}
\leanok
    By construction, $Z = \overline{\{-I\}} = \{-I^k \; | \; k \in \Z \} = \langle -I \rangle$, therefore $Z$ is generated by a single element and is thus cyclic.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
instance IsCyclic_Z : IsCyclic (Z F) := by
    apply isCyclic_iff_exists_orderOf_eq_natCard.mpr ?_
    by_cases h : NeZero (2 : F)
    · rw [card_Z_eq_two_of_two_ne_zero]
      use ⟨-1, neg_one_mem_Z ⟩
      simp
      exact orderOf_neg_one_eq_two
    · have two_eq_zero : (2 : F) = 0 := by exact not_neZero.mp h
      rw [card_Z_eq_one_of_two_eq_zero two_eq_zero]
      simp only [orderOf_eq_one_iff, exists_eq]
\end{verbatim}
\end{footnotesize}

\begin{remark}[Typeclass instances]
    Observe that we instead of telling Lean that \texttt{Is\_Cyclic\_Z} is a theorem we declare it to be
    an instance because we would like Lean to look for this fact whenever it requires it for theorem that may require
    the assumption that $Z$ is a commutative subgroup.
\end{remark}


In the next chapter it will be useful to record the interactions between $S$ and $Z$ for instance


\begin{definition}
    \label{SpecialSubgroups.SZ}
    \uses{SpecialSubgroups.S, SpecialSubgroups.Z, SpecialMatrices.s_mul_s_eq_s_add}
    \lean{SpecialSubgroups.SZ}
    \leanok
    We define the subgroup $SZ$ to be the subgroup with the underlying set $S \cup -S$, or equivalently the pointwise product $SZ$.
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def SZ (F : Type*) [Field F] : Subgroup SL(2,F) where
    carrier := { s σ | σ : F } ∪ { - s σ | σ : F }
    mul_mem' := by
      rintro x y (⟨σ₁, rfl⟩ | ⟨σ₁, rfl⟩) (⟨σ₂, rfl⟩ | ⟨σ₂, rfl⟩)
      repeat' simp
    one_mem' := by
      rw [← s_zero_eq_one]; left; use 0
    inv_mem' :=  by
      rintro x (⟨σ, rfl⟩ | ⟨σ, rfl⟩)
      repeat' simp
\end{verbatim}
\end{footnotesize}


\begin{corollary}
\label{SpecialSubgroups.S_mul_Z_subset_SZ}
\uses{SpecialSubgroups.SZ}
\lean{SpecialSubgroups.S_mul_Z_subset_SZ}
$SZ = S \cup -S$
\leanok
\end{corollary}
\begin{proof}
\leanok
    by construction an element of $SZ$ is either of the form $s_\sigma  I = s_\sigma \in S$ or $s_\sigma -I = -s_\sigma\in -S$. The reverse subset inclusion is very similar.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma S_mul_Z_subset_SZ :
    ((S F) : Set SL(2,F)) * ((Z F) : Set SL(2,F)) ⊆ ((SZ F) : Set SL(2,F)) := by
    rintro x ⟨t', ht', z, hz, hσ, h⟩
    obtain ⟨σ, rfl⟩ := ht'
    dsimp [Z] at hz
    dsimp
    rw [closure_neg_one_eq] at hz
    simp [SZ]
    rw [Set.mem_insert_iff, Set.mem_singleton_iff] at hz
    rcases hz with (rfl | rfl)
    left
    use σ
    rw [mul_one]
    right
    use σ
    simp
\end{verbatim}
\end{footnotesize}

\begin{lemma}
    \label{SpecialSubgroups.S_join_Z_eq_SZ}
    \uses{SpecialSubgroups.Z, SpecialSubgroups.S, SpecialSubgroups.SZ}
    \lean{SpecialSubgroups.S_join_Z_eq_SZ}
    \leanok
    The join of subgroups $S \sqcup Z = SZ$
\end{lemma}
\begin{proof}
\uses{SpecialSubgroups.closure_neg_one_eq, SpecialSubgroups.S_mul_Z_subset_SZ}
\leanok
We show that $S \sqcup Z = SZ$ by antisymetry, that is, we show both that
\begin{itemize}
    
    \item $S \sqcup Z \subseteq SZ$
    
    Let $x \in S \sqcup Z$, then if $x$ is in the subgroup closure then if $K$ is a subgroup whose underlying set contains $SZ$ then $x$ is in $K$,
    but since $SZ$ was shown to be a subgroup in \ref{SpecialSubgroups.SZ} we can conclude that $x \in SZ$ and thus $x = s_\sigma z$ for some $\sigma in F$.
    
    \item $SZ \subseteq S \sqcup Z$
    
    Let $s_\sigma z \in SZ$ then we must show that $s_\sigma z$ is the subgroup closure of $S$ and $Z$ but since the subgroup closure 
    must at least contain the pointwise product whose underlying set is equal to the union $S \cup -S$, we are done.
\end{itemize}
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma S_join_Z_eq_SZ : S F ⊔ Z F = SZ F := by
    ext x
    constructor
    · intro hx
      rw [sup_eq_closure_mul, mem_closure] at hx
      exact hx (SZ F) (S_mul_Z_subset_SZ)
    · rintro (⟨σ, rfl⟩ | ⟨σ, rfl⟩) <;> rw [sup_eq_closure_mul]
      · have mem_Z_mul_S : s σ ∈ ((S F) : Set SL(2,F)) * (Z F) := by
          rw [Set.mem_mul]
          use s σ
          split_ands
          simp [Z, closure_neg_one_eq]
          use σ
          simp
        apply Subgroup.subset_closure mem_Z_mul_S
      · have mem_Z_mul_T : -s σ ∈ ((S F) : Set SL(2,F)) * (Z F) := by
          rw [Set.mem_mul]
          use s σ
          split_ands
          simp [Z, closure_neg_one_eq]
          use σ
          simp
        apply Subgroup.subset_closure mem_Z_mul_T
\end{verbatim}
\end{footnotesize}

\section{Conjugacy of the Elements of $\SL_2(F)$}

\subsubsection{Classification of elements of $\SL_2(F)$ up to conjugation}

\begin{lemma}[Upper triangularizability criteria]
\label{isConj_upper_triangular_iff}
\lean{isConj_upper_triagnular_iff}
\leanok
    A matrix $M \in \textrm{Mat}_2(F)$ is triangularizable if and only if there exists an invertible matrix $C \in \GL_2(F)$ such that the bottom left entry
    $C M C^{-1}_{21} = 0$.
\end{lemma}

\begin{proof}
\leanok
    Given a matrix $U$ is in upper triangular form if and only if
    \[
    U = \begin{bmatrix}
    a & b\\
    0 & d
    \end{bmatrix}
    \]
    
    that is, the bottom left entry is zero. It then follows that 

    $M$ is triangularizable if and only there exists a $C \in \GL_2(F)$ such that is in upper triangular form $C M C^{-1}$, that is, the bottom left entry of $C M C^{-1}$ is zero. 
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma isConj_upper_triangular_iff [DecidableEq F] [IsAlgClosed F]
    {m : Matrix (Fin 2) (Fin 2) F} :
    (∃ a b d , ∃ (C : SL(2,F)), (C  * m * C⁻¹ : Matrix (Fin 2) (Fin 2) F) = !![a, b; 0, d]) ↔
      ∃ c : SL(2,F), ((c * m * (c⁻¹)) : Matrix (Fin 2) (Fin 2) F) 1 0 = 0 := by
    constructor
    · rintro ⟨a, b, d, c, hc⟩
      use c
      rw [hc]
      rfl
    · rintro ⟨c, hc⟩
      rw [← upper_triangular_iff_bottom_left_entry_eq_zero] at hc
      obtain ⟨a, b, d, h⟩ := hc
      use a, b, d
      use c
      rw [h]
\end{verbatim}
\end{footnotesize}

\begin{lemma}[Upper triangularizability of a $2 \times 2$ matrix over an algebraically closed field]
\label{isTriangularizable_of_algClosed}
\lean{isTriangularizable_of_algClosed}
\leanok
    When $F$ is an algebraically closed field, 
    for any $M \in \textrm{Mat}_2(F)$ there exists an invertible matrix $C \in \SL_2(F) \le \GL_2(F)$ such that $C M C^{-1} = U$ where
    \[
    U = \begin{bmatrix}
        a & b\\
        0 & d
    \end{bmatrix}\] for some $a, b, d \in F$.
\end{lemma}
\begin{proof}
    \uses{isConj_upper_triangular_iff}
    \leanok
We prove this by direct computation. 

Let 

\[
M = \begin{bmatrix}
\alpha & \beta\\
\gamma & \delta
\end{bmatrix} \in \textrm{Mat}_2(F)
\]

By lemma \ref{isConj_upper_triangular_iff}, we only need to show that we can find a matrix $C \in \SL_2(F)$ such that when it acts on $M$ by conjugation, the bottom left entry is annihilated.

\begin{itemize}
    \item Suppose on the one hand that $\beta \ne 0$
    
    Observe that 
    
    \begin{equation}\label{triang}
        s_\sigma M s_\sigma^{-1} = \left(\begin{bmatrix}
            -\beta \sigma + \alpha & \beta \\
            -\beta \sigma^{2} + \alpha \sigma - \delta \sigma + \gamma & \beta \sigma + \delta
            \end{bmatrix}\right)
    \end{equation}

    Given $F$ is algebraically closed we can set $\sigma \in F$ to be a root of the polynomial

    \[
    P(X) := -\beta X^{2} + \alpha X - \delta X + \gamma 
    \]

    setting $C := s_\sigma$ yields the desired element which triangularizes $M$.
    

    \item Suppose on the other hand that $\beta = 0$
    
    Given the top right entry is zero we only need find a matrix in $\SL_2(F)$ which flips the antidiagonal entries (modulo modifying the signs)
    it is thus sufficient to use 
        \[
        w = \begin{bmatrix}
        0 & -1\\
        1 & 0
        \end{bmatrix} \quad \text{as indeed} \quad w M w^{-1} = \begin{bmatrix}
            \delta & -\gamma\\
            0 & \alpha
        \end{bmatrix} \text{ is in triangular form} 
        \]
\end{itemize}

\end{proof}
\begin{footnotesize}
\begin{verbatim}
theorem isTriangularizable_of_algClosed [DecidableEq F] [IsAlgClosed F]
    (M : Matrix (Fin 2) (Fin 2) F) :
    ∃ a b d, ∃ (C : SL(2,F)), (C * M * C⁻¹ : Matrix (Fin 2) (Fin 2) F) = !![a, b; 0, d] := by
    let α := M 0 0
    let β := M 0 1
    let γ := M 1 0
    let δ := M 1 1
    have M_coe_eq : M = !![α, β; γ, δ] := by ext <;> rfl
    -- Is conjugate to an upper triangular matrix iff there exists a matrix such that
    -- when conjugated kills the bottom left entry
    rw [isConj_upper_triangular_iff]
    -- If β ≠ 0 then we solve the quadratic to force the bottom left entry to be 0
    by_cases hβ : β ≠ 0
    · obtain ⟨σ, hσ⟩ := exists_root_of_special_poly α β γ δ hβ
      use s σ
      simp [M_coe_eq, s, Matrix.conj_s_eq]
      ring_nf at hσ ⊢
      exact hσ
    simp at hβ
    · use w
      simp [M_coe_eq, w, inv_def, hβ]
\end{verbatim}
\end{footnotesize}

\begin{corollary}[Upper triangular matrices are conjugate to lower triangular matrices]
    \label{lower_triangular_isConj_upper_triangular}
    \uses{SpecialMatrices.w}
    \lean{lower_triangular_isConj_upper_triangular}
    \leanok
    For every $U \in \textrm{Mat}_2(F)$ that is upper triangular the matrix $w U w^{-1}$ is a lower triangular matrix
\end{corollary}
\begin{proof}
    \leanok
    Direct computation shows this result, see the Lean code!
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma lower_triangular_isConj_upper_triangular {a b : F} :
    ∃ C : SL(2,F), C * !![a, 0; -b, a] * C⁻¹ = !![a, b; 0, a] := by
    use w
    simp [w]
\end{verbatim}
\end{footnotesize}

\begin{lemma}
    \label{upper_triangular_isConj_diagonal_of_nonzero_det}
    \lean{upper_triangular_isConj_diagonal_of_nonzero_det}
    \leanok
    An upper triangular matrix $U = \begin{bmatrix}
        \alpha & \beta\\
        0 & \delta
    \end{bmatrix}$ is conjugate to a diagonal matrix if $\alpha - \delta \ne 0$
\end{lemma}
\begin{proof}
    \leanok
We show this by direct computation.

Conjugation of $M$ by the matrix 

\[
C := \begin{bmatrix}
    1 & \frac{\beta}{\alpha - \delta}\\
    0 & 1
\end{bmatrix}
\]

yields a diagonal matrix (see the Lean code for the computation!).
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma upper_triangular_isConj_diagonal_of_nonzero_det  [DecidableEq F]
    {a b d : F} (had : a - d ≠ 0) : ∃ C : SL(2,F), C * !![a, b; 0, d] * C⁻¹ = !![a, 0; 0, d] := by
    use ⟨!![1, b / (a - d); 0, 1], by simp⟩
    simp
    ext
    repeat'
    field_simp
    ring_nf
\end{verbatim}
\end{footnotesize}

% \begin{remark}[Automation in Lean]
%     Observe that the proof of the theorem above primarily uses tactics! Tactics l 
% \end{remark}


\begin{proposition}
\label{SL2_IsConj_d_or_IsConj_s_or_IsConj_neg_s_of_AlgClosed}
\uses{SpecialMatrices.s, SpecialMatrices.d}
\lean{SL2_IsConj_d_or_IsConj_s_or_IsConj_neg_s_of_AlgClosed}
\leanok
    Each element of $\SL_2(F)$ is conjugate to either $d_\delta$ for some $\delta \in F^\times$, or to $\pm s_\sigma$ for some $\sigma \in F$.
\end{proposition}

\begin{proof}
\uses{isTriangularizable_of_algClosed, lower_triangular_isConj_upper_triangular, upper_triangular_isConj_diagonal_of_nonzero_det}
\leanok Since $F$ is algebraically closed, any element $x \in \SL_2(F)$can be regarded as a linear transformation in the 2 dimensional vector space over $F$, with the eigenvalues $\pi_1$ and $\pi_2$. \\
\\
\space If $\pi_1$ and $\pi_2$ are distinct, then $x$ is thus diagonalisable. That is, there exists an invertible matrix $a \in GL(2, F)$ such that $y = axa^{-1}$ is a diagonal matrix. Furthermore, we can multiply $a$ by a suitable scalar to find an element in $\SL_2(F)$which conjugates $x$ and $y$:

\begin{align*}
    \text{Set} \; b = \frac{a}{\sqrt {\text{det}(a)}}, \quad \text{thus } \; bxb^{-1} =\frac{a}{\sqrt {\text{det}(a)}} \; x \; (\sqrt{\text{det}(a)} \; )\,a^{-1} = axa^{-1} = y.
\end{align*}

Observe that det$(b)=1$, hence $x$ and $y$ are conjugate in $L$. Furthermore, since $y$ is a diagonal matrix it must belong to the set $D$, showing that $x$ is conjugate to $d_\delta$ for some $\delta \in F^\times$. \\
\\
\space If $\pi_1 = \pi_2$ then $x$ has just one repeated eigenvalue. Suppose that $x$ is diagonalisable. Then there exists an element $c \in GL(2, F)$ and a diagonal matrix $\pi_1 I_G$ such that $x = c(\pi_1 I_G)c^{-1} = \pi_1 I_G$. Thus $x = \pm I_G$, which trivially belongs to both $D$ and $\times Z$. \\
\\
Now assume that $x$ is not diagonalisable. Chapter 7 of \cite{matrix} shows that there exists an element $d \in GL(2, F)$, such that $x= djd^{-1}$, where, $$j = \begin{bmatrix} \pi_1 & 1 \\ 0 & \pi_1 \end{bmatrix}$$ is the Jordan Normal Form of $x$. By the method described above, we can multiply $d$ by a suitable scalar to show that $x$ is conjugate to $j$ in $L$. Now we conjugate $j$ by an element of $\SL_2(F)$whose top left entry is 0.

\begin{align*}
    \begin{bmatrix} 0 & -\gamma^{-1} \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} \pi_1 & 1 \\ 0 & \pi_1 \end{bmatrix} \begin{bmatrix} \delta & \gamma^{-1} \\ -\gamma & 0 \end{bmatrix} = \begin{bmatrix} 0 & -\gamma^{-1} \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} \pi_1 \delta - \gamma & \pi_1 \gamma^{-1} \\ -\pi_1 \gamma & 0 \end{bmatrix} = \begin{bmatrix} \pi_1 & 0 \\ -\gamma^{2} & \pi_1 \end{bmatrix}
\end{align*}
\\
Now clearly the determinant of $x$ is equal to the determinant of $j$, namely 1, which means that $\pi_1 = \pm 1$. This shows that $j$ is conjugate in $\SL_2(F)$to some element in $\times Z$ as well as $x$. Furthermore, since conjugation is transitive, $x$ is conjugate to $\pm s_\sigma$ for some $\sigma \in F$.

\end{proof}
\begin{footnotesize}
\begin{verbatim}
theorem SL2_IsConj_d_or_IsConj_s_or_IsConj_neg_s_of_AlgClosed [DecidableEq F] [IsAlgClosed F]
    (S : SL(2, F)) :
    (∃ δ : Fˣ, IsConj (d δ) S)
    ∨
    (∃ σ : F, IsConj (s σ) S)
    ∨
    (∃ σ : F, IsConj (- s σ) S) := by
    -- S is conjugate to an upper triangular matrix
    have S_IsConj_upper_triangular :
      ∃ a b d, ∃ C : SL(2,F), (C * S * C⁻¹ : Matrix (Fin 2) (Fin 2) F) = !![a, b; 0, d] :=
      isTriangularizable_of_algClosed (S : Matrix (Fin 2) (Fin 2) F)
    have det_coe_S_eq_one : det (S : Matrix (Fin 2) (Fin 2) F ) = 1 := by simp
    obtain ⟨a, b, d, C, h⟩ := S_IsConj_upper_triangular
    -- Because !![a, b; 0, d] is conjugate to S it also has determinant 1
    have det_eq_one : det !![a, b; 0, d] = 1 := by
      rw [← det_coe_S_eq_one, ← h]
      simp only [det_mul, SpecialLinearGroup.det_coe, mul_one, one_mul]
    have had := det_eq_one
    -- The determinant being equal to 1 implies a * d = 1
    simp at had
    -- so the inverse of a is equal to d
    have d_eq_inv_a : d = a⁻¹ := Eq.symm (DivisionMonoid.inv_eq_of_mul a d had)
    -- Therefore a is a unit
    have a_is_unit : IsUnit a := by exact isUnit_of_mul_eq_one a d had
    -- Furthermore, a is nonzero
    have a_ne_zero : a ≠ 0 := by exact left_ne_zero_of_mul_eq_one had
    have det_eq_one' : det !![a, 0; 0, d] = 1 := by simp [d_eq_inv_a]; rw [mul_inv_cancel₀ a_ne_zero]
    obtain rfl | had' := eq_or_ne a d
    · right
      simp [← sq] at det_eq_one'
      rcases det_eq_one' with (a_eq_one | a_eq_neg_one)
      · left
        rw [a_eq_one] at h
        have det_eq_one'' : det !![1, b; 0, 1] = 1 := by norm_num
        use -b
        have isConj₁ : ∃ C : SL(2,F), C * (s (-b)) * C⁻¹ = ⟨!![1, b; 0, 1], det_eq_one''⟩ := by
          apply IsConj_coe
          exact lower_triangular_isConj_upper_triangular
        have isConj₂ : ∃ C : SL(2,F), C * S * C⁻¹ = ⟨!![1, b; 0, 1], det_eq_one''⟩ := by
          use C
          apply SpecialLinearGroup.eq_of
          simp only [SpecialLinearGroup.coe_mul, h]
        rw [← isConj_iff] at isConj₁ isConj₂
        apply IsConj.trans isConj₁ isConj₂.symm
      · right
        rw [a_eq_neg_one] at h
        have det_eq_one'' : det !![-1, b; 0, -1] = 1 := by norm_num
        have S_eq : - s b = !![-1, 0; -b, -1] := by simp [s]
        use b
        have isConj₁ : ∃ C : SL(2,F), C * (-s b) * C⁻¹ = ⟨!![-1, b; 0, -1], det_eq_one''⟩ := by
          apply IsConj_coe
          simp only [S_eq]
          exact lower_triangular_isConj_upper_triangular
        have isConj₂ : ∃ C : SL(2,F), C * S * C⁻¹ = ⟨!![-1, b; 0, -1], det_eq_one''⟩ := by
          use C
          apply SpecialLinearGroup.eq_of
          simp only [SpecialLinearGroup.coe_mul, h]
        rw [← isConj_iff] at isConj₁ isConj₂
        -- conjugation is transitive
        apply IsConj.trans isConj₁ isConj₂.symm
    · left
      use a_is_unit.unit
      have isConj₁ : ∃ C : SL(2,F), C * S * C⁻¹ =  ⟨!![a, b ; 0, d], det_eq_one⟩ := by
        use C
        apply SpecialLinearGroup.eq_of
        simp only [SpecialLinearGroup.coe_mul]
        rw [h]
      have isConj₂ :
        ∃ C : SL(2,F), C * ⟨!![a, b; 0,d], det_eq_one⟩ * C⁻¹ = ⟨!![a,0;0,d], det_eq_one'⟩ := by
        apply IsConj_coe
        refine upper_triangular_isConj_diagonal_of_nonzero_det ?a_ne_d
        intro h
        rw [sub_eq_zero] at h
        contradiction
      simp_rw [← isConj_iff, d_eq_inv_a] at isConj₁ isConj₂
      simp only [SpecialMatrices.d, IsUnit.unit_spec]
      -- conjugation is transitive
      apply IsConj.trans isConj₂.symm isConj₁.symm
\end{verbatim}
\end{footnotesize}

\begin{remark}
Formalizing the classification of elements of $\SL_2(F)$ up to conjugation in Lean was surprisingly difficult.

Given the (informal) proof presented of proposition \ref{SL2_IsConj_d_or_IsConj_s_or_IsConj_neg_s_of_AlgClosed} extracted from Christopher Butler's exposition uses the Jordan Normal Form theorem. 

Furthermore, since at the time of writing, the Jordan Normal form theorem is still not yet in \texttt{mathlib} this theorem turned out to be quite difficult to formalise.

The original approach to formalise the Jordan Normal Form theorem for $2 \times 2$ matrices involved studying the eigenspace and generalized eigenspaces of the endomorphism associated to a $2 \times 2$ matrix.
This is one the standard approached taught in an undergraduate curriculum, yet surprisingly, to formalise the $2 \times 2$ case with this approach untractable.

The reason this approach and often standard techniques might not integrate well with \texttt{mathlib} often occurs for the following reasons I will now outline.

The crux of formalising a mathematical result always lies at finding the right abstraction, as illustrated in \ref{lattice}, understanding the lattice structure on the set of subgroups becomes an indispensable tool for formalising
results regarding subgroups and their properties. In this particular case, the right abstraction is not entirely clear.

Is it best to prove the theorem for matrices or for endomoprhisms? Which will be the easiest approach? Which approach is most general? Which approach yields the most amount of useful lemmas?

The reason why the Jordan Normal Form theorem is not yet in \texttt{mathlib} is because it hinges on the following two results which have not been formalised yet:

\begin{enumerate}
    \item The classification of nilpotent endomorphisms.
    \item The classification of semisimple endomorphisms.
\end{enumerate}

Such formalisation would be an amazing project to undertake. Bear in mind, the theorem formalized is the more general Jordan-Chevallier theorem.

To the authors understanding, the general theorem will be formalised by studying the eigenspace and general eigenspace. This approach turned out to be essentially equivalent in 
difficulty to formalising the special case of $2 \times 2$ matrices over an algebraically closed field with the same approach,
since the argument is inductive in some sense.


Therefore, after discussions with Prof. Kevin Buzzard's it turned out to be much more effective to classify matrices of the special linear group up to 
conjugation by splitting on a few different cases of what a $2 \times 2$ matrix might look like and finding the suitable matrices by which to conjugate to put them in either the form of $d_\delta$ or $\pm s_\sigma$.
\end{remark}

\section{Centralizers \& Normalizers}

Both the centralizer and normalizer of a subset $H$ are subgroups of $G$. Note also that the centralizer is a stronger condition than the 
normalizer and any element in the centralizer of $H$ is also in its normalizer. If $H$ is a singleton then it's clear that its centralizer and normalizer are equal.\\

\subsubsection{Normalizers}

\begin{definition}
The \textbf{normalizer} $N_G(H)$ of a subset $H$ of a group $G$ is the set of elements of $G$ which stabilise $H$ under conjugation.
\begin{equation*} N_G(H) = \{ g \in G : gHg^{-1}=H\}. \end{equation*}
\end{definition}


\begin{corollary}
    \label{lower_triangular_iff_top_right_entry_eq_zero}
    \lean{lower_triangular_iff_top_right_entry_eq_zero}
    \leanok
    A matrix $M \in \textrm{Mat}(2; F)$ is lower triangular if and only if the $M_{12} = 0$.
\end{corollary}
\begin{proof}
    \leanok
It is easy to see the top right entry must be zero for a matrix to be lower triangular.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma lower_triangular_iff_top_right_entry_eq_zero {M : Matrix (Fin 2) (Fin 2) F} :
    (∃ a c d, !![a, 0; c, d] = M) ↔ M 0 1 = 0 := by
    constructor
    · rintro  ⟨a, b, d, hM⟩
      simp [← hM]
    · intro h
      use M 0 0, M 1 0, M 1 1
      simp_rw [← h]
      ext <;> rfl
\end{verbatim}
\end{footnotesize}

\begin{proposition}[Normalizer of subgroups of $S$ are contained in $L$]
\label{normalizer_subgroup_S_le_L}
\uses{SpecialSubgroups.S, SpecialSubgroups.L}
\lean{normalizer_subgroup_S_le_L}
\leanok
 For any subgroup $S_0 \leq S$ with order greater than 1, we have that the normalizer $N_{\SL_2(F)}(S_0) \subset L$.
\end{proposition}
\begin{proof}
    \uses{mem_L_iff_lower_triangular, lower_triangular_iff_top_right_entry_eq_zero}
    \leanok
Let $s_\sigma$ be an arbitary element of $S_0$ with $\sigma \neq 0$. To determine the normalizer of $S_0$ in $\SL_2(F)$we consider which $x \in \SL_2(F)$ satisfy $x s_\sigma x^{-1} \in S_0$.
\begin{align*} x s_\sigma x^{-1} &= \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} 1 & 0 \\ \sigma & 1 \end{bmatrix} \begin{bmatrix} \delta & - \beta \\ - \gamma & \alpha \end{bmatrix}
\\[1.5ex] &= \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} \delta & - \beta \\ \delta \sigma - \gamma & \alpha - \beta \sigma \end{bmatrix}
\\[1.5ex] &= \begin{bmatrix} \alpha \delta - \beta \gamma + \beta \delta \sigma & - \beta^2  \sigma \\ \delta^2 \sigma & \alpha \delta - \beta \gamma - \beta \delta \sigma \end{bmatrix}.
\end{align*}
Since $x s_\sigma x^{-1} \in S_0$ we have $- \beta^2  \sigma = 0$ and since $\sigma \neq 0$, we have $\beta = 0$. Since $s_\sigma$ was chosen arbitrarily, any element which normalises $S_0$ is a lower diagonal matrix and is therefore in $H$ by (\ref{Hlowertri}). Thus $N_{\SL_2(F)}(S_0) \subset H$ as required. \\
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma normalizer_subgroup_S_le_L [DecidableEq F] { S₀ : Subgroup (SL(2,F)) }
    (hT₀ : 1 < Nat.card S₀ ) (h : S₀ ≤ S F) : normalizer S₀ ≤ L F := by
    intro x hx
    rw [mem_normalizer_iff] at hx
    by_cases h' : ∃ σ, σ ≠ 0 ∧ s σ ∈ S₀
    · obtain ⟨σ, σ_ne_zero, hσ⟩  := h'
      specialize hx (s σ)
      rw [hx] at hσ
      let α := x.1 0 0
      let β := x.1 0 1
      let γ := x.1 1 0
      let δ := x.1 1 1
      have x_eq : x = !![α, β; γ, δ] := by ext <;> rfl
      have : x * s σ * x⁻¹ ∈ S F := by exact h hσ
      obtain ⟨σ' , hσ'⟩ := this
      simp [x_eq] at hσ'
      -- uses decidable equality
      rw [SpecialSubgroups.mem_L_iff_lower_triangular, lower_triangular_iff_top_right_entry_eq_zero]
      have β_eq_zero : s σ' 0 1 = 0 := by simp [s]
      rw [hσ'] at β_eq_zero
      simp [x_eq, s] at β_eq_zero
      ring_nf at β_eq_zero
      rw [neg_eq_zero] at β_eq_zero
      apply eq_zero_of_ne_zero_of_mul_right_eq_zero σ_ne_zero at β_eq_zero
      rw [sq_eq_zero_iff] at β_eq_zero
      simp [x_eq]
      exact β_eq_zero
    · push_neg at h'
      have S₀_eq_bot : S₀ = ⊥ := by
        rw [eq_bot_iff_forall]
        intro x hx
        obtain ⟨σ, rfl⟩ := h hx
        specialize h' σ
        rw [not_imp_not] at h'
        specialize h' hx
        simp [h']
      have : Nat.card S₀ = 1 := by simp [S₀_eq_bot]
      -- contradiction with the assumption that Nat.card S₀ > 1
      linarith
\end{verbatim}
\end{footnotesize}

\begin{lemma}
    \label{ex_of_card_D_gt_two}
    \lean{ex_of_card_D_gt_two}
    \leanok
    If the cardinality of finite subgroup of $D_0 \le D$ is greater than $2$ then there exists an element $x \in D_0$ which does not belong to the center $Z$, that is, $x \ne d_1 = I$ and $x \ne d_{-1} = -I$.
\end{lemma}
\begin{proof}
    \leanok
 Suppose for a contradiction that if $\delta \ne \pm 1$ then $d_\delta \notin D_0$. We show that $D_0 \le Z$ and therefore, $|D_0| \le 2$ our contradiction.
 
 Let $d_\delta \in D_0 \le D$ then given $d_\delta \notin D_0$ if $\delta \ne \pm 1$ and $Z = \langle -I\rangle = \{I, -I\}$. It immediately follows that $D_0 \le Z$.

\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma ex_of_card_D_gt_two {D₀ : Subgroup SL(2,F) }(hD₀ : 2 < Nat.card D₀) (D₀_leq_D : D₀ ≤ D F) :
    ∃ δ : Fˣ, (δ : F) ≠ 1 ∧ (δ : F) ≠ -1 ∧ d δ ∈ D₀ := by
    by_contra! h
    have D₀_le_Z : D₀.carrier ≤ Z F := by
      simp
      intro x hx
      obtain ⟨δ, rfl⟩ := D₀_leq_D hx
      rw [Set.mem_insert_iff]
      by_cases h₀ : (δ : F) = 1
      · left;
        rw [Units.val_eq_one] at h₀
        rw [h₀, d_one_eq_one]
      · by_cases h₁ : (δ : F) = -1
        · right
          rw [val_eq_neg_one] at h₁
          rw [h₁, d_neg_one_eq_neg_one, Set.mem_singleton_iff]
        · rw [← ne_eq] at h₀ h₁
          specialize h δ h₀ h₁
          contradiction
    have card_D₀_leq_two : Nat.card D₀ ≤ 2 :=
      le_trans (Subgroup.card_le_of_le D₀_le_Z) card_Z_le_two
    linarith
\end{verbatim}
\end{footnotesize}

\begin{proposition}[Normalizers of subgroups of $D$ are contained in $L$]
\label{normalizer_subgroup_D_eq_DW}
\uses{SpecialSubgroups.D, SpecialSubgroups.DW}
\lean{normalizer_subgroup_D_eq_DW}
\leanok
    $N_{\SL_2(F)}(D_0) = \langle D , w \rangle$, where  $D_0$ is any subgroup of $D$ with order greater than 2. \\
    \end{proposition}

\begin{proof}
    \uses{SpecialLinearGroup.fin_two_diagonal_iff, SpecialLinearGroup.fin_two_antidiagonal_iff, ex_of_card_D_gt_two}
    \leanok
    Since $|D_0| > 3$, we can choose a $d_\delta \in D_0 \! \setminus \! Z$, that is where $\delta \neq 1$. To determine the normalizer of $D_0$ in $\SL_2(F)$we consider which $x \in \SL_2(F)$satisfy $x d_\delta x^{-1} \in D_0$.
    \begin{align}\label{6.3proof3} xd_\delta x^{-1} &= \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \begin{bmatrix} \delta & - \beta \\ - \gamma & \alpha \end{bmatrix} \nonumber \\[1.5ex]
    &= \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} \delta \delta & - \beta \delta \\ - \gamma \delta^{-1} & \alpha \delta^{-1} \end{bmatrix} \nonumber \\[1.5ex]
    &= \begin{bmatrix} \alpha \delta \delta - \beta \gamma \delta^{-1} & \alpha \beta (\delta^{-1} - \delta) \\ \gamma \delta (\delta - \delta^{-1}) & \alpha \delta \delta^{-1} - \beta \gamma \delta \end{bmatrix} \in D_0.
    \end{align}
    
    Since (\ref{6.3proof3}) is in $D_0$, the top right and bottom left entries must be 0. Since  $\delta \neq \pm 1$, we have $\delta \neq \delta^{-1}$ and so $\alpha \beta = 0 = \gamma \delta$. \\
    \\
     \space If $\alpha = 0$, then $\beta$ and $\gamma$ are non-zero since det$(x) = 1$, thus $\delta = 0$. So det$(x) = - \gamma \beta = 1$  and $- \gamma = \beta^{-1}$. (\ref{6.3proof3}) becomes $$\begin{bmatrix} \delta^{-1} & 0 \\ 0 & \delta \end{bmatrix} = d^{-1}_\delta.$$Since $D_0$ is a group, it contains the inverse of each of it's elements, so $d^{-1}_\delta \in D_0$ as required. In this case we have $x \in wD$. \\
    \\
     \space If $\alpha \neq 0$, then similarly $\beta = 0$, $\delta = \alpha^{-1}$ and $\gamma = 0$. (\ref{6.3proof3}) now becomes $$\begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} = d_\delta \in D_0.$$This time we have $x \in D$. So $x \in D \cup wD = \langle D , w \rangle$ and any element which normalises $D_0$ is in $\langle D , w \rangle$, thus $N_{\SL_2(F)}(D_0) \subset \langle D , w \rangle$. \\
    \\
    Now take an arbitrary $y \in \langle D , w \rangle = D \cup wD$. If $y \in D$ then $y = d_{\rho 1}$, for some $\rho 1 \in F^\times$.
    \begin{align*} d_{\rho 1} d_\delta d^{-1}_{\rho 1} = d_\delta \in D_0. \tag{by Lemma \ref{6.1}}
    \end{align*}
    
    If $y \in wD$ then $y = w d_{\rho 2}$, for some $ d_{\rho 2} \in F^\times$.
    \begin{align*} (w d_{\rho 2}) d_\delta (w d_{\rho 2})^{-1} &= w d_{\rho 2} d_\delta d^{-1}_{\rho 2} w^{-1}
    \\ &= w d_\delta w^{-1}
    \\ &= d^{-1}_\delta \in D_0 \tag{by Lemma \ref{6.1}}.
    \end{align*}
    
    Thus $y$ indeed who whole of $\langle D , w \rangle$ is contained in $N_{\SL_2(F)}(D_0)$. This inclusion gives the desired result, $N_{\SL_2(F)}(D_0) = \langle D , w \rangle$. \\
    
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma normalizer_subgroup_D_eq_DW { D₀ : Subgroup (SL(2,F)) }
    (hD₀ : 2 < Nat.card D₀ ) (D₀_leq_D : D₀ ≤ D F) : normalizer D₀ ≤ DW F := by
     intro x hx
     rw [mem_normalizer_iff] at hx
     have ⟨δ', h₀, h₁, hδ'⟩ := ex_of_card_D_gt_two hD₀ D₀_leq_D
     specialize hx (d δ')
     rw [hx] at hδ'
     have mem_D := D₀_leq_D hδ'
     rw [mem_D_iff, ← SpecialLinearGroup.fin_two_diagonal_iff] at mem_D
     rcases get_entries x with ⟨α, β, γ, δ, hα, hβ, hγ, hδ, x_eq⟩
     rcases mem_D with ⟨top_right, bottom_left⟩
     simp [d, x_eq] at top_right bottom_left
     ring_nf at top_right bottom_left
     have top_right_eq : -(α * (δ' : F) * β) + α * β * (δ' : F)⁻¹ = α * β * ((↑δ')⁻¹ - ↑δ') := by ring
     have bottom_left_eq : (δ' : F) * γ * δ - (δ' : F)⁻¹ * γ * δ  = γ * δ * (↑δ' - (↑δ')⁻¹) := by ring
     replace top_right := top_right_eq ▸ top_right
     replace bottom_left := bottom_left_eq ▸ bottom_left
     have det_eq_one : det (x : Matrix (Fin 2) (Fin 2) F) = 1 := by rw [SpecialLinearGroup.det_coe]
     have δ_sub_δ_inv_ne_zero : (δ' : F)⁻¹ - δ' ≠ 0 := by
       field_simp
       intro h
       rw [sub_eq_zero, ← sq] at h
       symm at h
       rw [sq_eq_one_iff] at h
       apply not_or_intro h₀ h₁ h
     have δ_inv_neg_δ_ne_zero : (δ') - (δ' : F)⁻¹ ≠ 0 := by
       rw [← neg_ne_zero, neg_sub]; exact δ_sub_δ_inv_ne_zero
     have α_or_β_eq_zero : α * β = 0 :=
       eq_zero_of_ne_zero_of_mul_right_eq_zero δ_sub_δ_inv_ne_zero top_right
     have γ_or_δ_eq_zero : γ * δ = 0 :=
       eq_zero_of_ne_zero_of_mul_right_eq_zero δ_inv_neg_δ_ne_zero bottom_left
     rw [mul_eq_zero] at α_or_β_eq_zero γ_or_δ_eq_zero
     rcases α_or_β_eq_zero with (α_eq_zero | β_eq_zero) <;>
     rcases γ_or_δ_eq_zero with (γ_eq_zero | δ_eq_zero)
     · have det_eq_zero : det (x : Matrix (Fin 2) (Fin 2) F) = 0 := by
         rw [det_fin_two, ← hα, ← hγ, α_eq_zero, γ_eq_zero, mul_zero, zero_mul, sub_zero]
       rw [det_eq_zero] at det_eq_one
       absurd zero_ne_one det_eq_one
       trivial
     · apply Dw_leq_DW
       rw [mem_D_w_iff, ← SpecialLinearGroup.fin_two_antidiagonal_iff]
       simp_rw [← hα, ← hδ, α_eq_zero, δ_eq_zero]
       trivial
     · apply D_leq_DW
       rw [mem_D_iff, ← SpecialLinearGroup.fin_two_diagonal_iff]
       simp_rw [← hβ, ← hγ, β_eq_zero, γ_eq_zero]
       trivial
     · have det_eq_zero : det (x : Matrix (Fin 2) (Fin 2) F) = 0 := by
         rw [det_fin_two, ← hβ, ← hδ, β_eq_zero, δ_eq_zero, mul_zero, zero_mul, sub_zero]
       rw [det_eq_zero] at det_eq_one
       absurd zero_ne_one det_eq_one
       trivial
\end{verbatim}
\end{footnotesize}

%-----------------------------

\subsubsection{Centralisers}

\begin{definition}[Centralizer]
The \textbf{centralizer} $C_G(H)$ of a subset $H$ of a group $G$ is the set of elements of $G$ which commute with each element of $H$.
\begin{equation*} 
    C_G(H) = \{ g \in G  : gh=hg, \quad \forall h\in L \}. \end{equation*} 
\end{definition}

\begin{corollary}
    \label{centralizer_neg_eq_centralizer}
    \lean{centralizer_neg_eq_centralizer}
    \leanok
    Let $x \in \SL_2(F)$ then the centralizer of the negative equals $C_{SL_2(F)}(x) = C_{SL_2(F)}(-x)$.
\end{corollary}
\begin{proof}
    \leanok
 An element $y \in \SL_2(F)$ belongs to $C_{SL_2(F)}$ if and only if
  $1 = x y x^{-1} y^{-1} = (-x) y (-x^{-1}) y^{-1}$  if and only if $y$ belongs to $C_{\SL_2(F)}(-x)$.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma centralizer_neg_eq_centralizer {x : SL(2,F)} : centralizer {x} = centralizer {-x} := by
    ext; constructor <;> simp [mem_centralizer_iff_commutator_eq_one]
\end{verbatim}
\end{footnotesize}
    

\begin{proposition}[Centralizer of noncenter $s_\sigma$]
\label{centralizer_s_eq_SZ}
\uses{SpecialSubgroups.S, SpecialSubgroups.Z, SpecialMatrices.s}
\lean{centralizer_s_eq_SZ}
\leanok
The centralizer $C_{\SL_2(F)}(\pm s_\sigma) =  S \times Z $ where $\sigma \neq 0$.
\end{proposition}

\begin{proof}
    \uses{SpecialLinearGroup.fin_two_shear_iff, centralizer_neg_eq_centralizer}
    \leanok
To determine the centralizer of $s_\sigma$ in $L$, we consider which $y \in \SL_2(F)$satisfy $y s_\sigma = s_\sigma y$ for an arbitrarily chosen $s_\sigma$, with $\sigma \neq 0$. \\
\vspace{-0.5mm}
\begin{align}\label{6.3proof2} y s_\sigma &= s_\sigma y, \nonumber \\[1.5ex]
\begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} 1 & 0 \\ \sigma & 1 \end{bmatrix} &= \begin{bmatrix} 1 & 0 \\ \sigma & 1 \end{bmatrix} \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix}, \nonumber \\[1.5ex]
\begin{bmatrix} \alpha + \beta \sigma & \beta \\ \gamma + \delta \sigma & \delta \end{bmatrix} &= \begin{bmatrix} \alpha & \beta \\ \gamma +  \alpha \sigma & \delta + \beta \sigma \end{bmatrix}.
\end{align}
\vspace{.5mm}

Equating the top left entries of (\ref{6.3proof2}) gives $\alpha + \beta \sigma = \alpha$ which means $\beta = 0$ since $\sigma \neq 0$ by assumption. Equating the bottom left entries gives that $\alpha = \delta$. Finally, since det$(y) = 1$, we have $\alpha \delta = 1$ so $\alpha = \pm 1$. Thus a $y \in C_{\SL_2(F)}(s_\sigma)$ is

\begin{align*} y &= \begin{bmatrix} \alpha & 0 \\ \gamma & \alpha \end{bmatrix}. \tag{where $\alpha = \pm 1$}
\end{align*}

So $y = \pm s_\sigma$ for some $\sigma \in F$, and $SZ = \{ \pm s_\sigma \} \subset C_{\SL_2(F)}(s_\sigma)$. Now take an arbitrary $s_\gamma z \in SZ$.
\begin{align*} (s_\gamma z) s_\sigma &= s_\sigma (s_\gamma z),
\\ s_\gamma s_\sigma z &= s_\sigma s_\gamma z, \tag{since $z \in Z$}
\\ s_{\gamma + \sigma} &= s_{\gamma + \sigma}.
\end{align*}

Thus $s_\gamma z$ and indeed the whole of $SZ$ is contained in $ C_{\SL_2(F)}(s_\sigma)$, so $C_{\SL_2(F)}(s_\sigma) = SZ$. \\
\\
Since $S$ commutes elementwise with $Z$ and $\cap Z = \{ I_G \}$, we can apply Corollary \ref{directproductZ} and assert that $C_{\SL_2(F)}(s_\sigma) = SZ \cong S \times Z$ as required. The centralizer of $- s_\sigma$ is also $\times Z$, since an element $x$ commutes with $- s_\sigma$ if and only if it commutes with $s_\sigma$:
\begin{align*} 
    xs_\sigma = s_\sigma x \iff -(x s_\sigma) = - (s_\sigma x) \iff x(- s_\sigma) = (- s_\sigma)x.
\end{align*}

Note that in case of $\sigma = 0$, $\pm s_\sigma \in Z$ and thus it's centralizer is the whole of $L$.

\end{proof}
\begin{footnotesize}
\begin{verbatim}
theorem centralizer_s_eq_SZ {σ : F} (hσ : σ ≠ 0) : centralizer { s σ } = SZ F := by
    ext x
    constructor
    · intro hx
      simp [mem_centralizer_iff] at hx
      rw [SpecialLinearGroup.fin_two_ext_iff] at hx
      simp [s] at hx
      obtain ⟨top_right, -, bottom_left, -⟩ := hx
      rcases get_entries x with ⟨α, β, γ, δ, hα, hβ, -, hδ, x_eq⟩
      simp [x_eq, hσ] at top_right bottom_left
      rw [add_comm γ] at bottom_left
      have α_eq_δ : σ * α = σ * δ := by rw [mul_comm σ δ, eq_iff_eq_of_add_eq_add bottom_left]
      rw [mul_eq_mul_left_iff, or_iff_not_imp_right] at α_eq_δ
      specialize α_eq_δ hσ
      simp [SZ]
      -- is a shear matrix if diagonal entries are equal and top right entry is zero
      rw [← SpecialLinearGroup.fin_two_shear_iff]
      constructor
      -- diagonal entries are equal
      · rw [← hα, ← hδ, α_eq_δ]
      -- top right entry is zero
      · rw [← hβ, top_right]
    · rintro (⟨σ, rfl⟩ | ⟨σ, rfl⟩)
      repeat
      rw [mem_centralizer_iff]
      intro y hy
      simp at hy
      rw [hy]
      simp [add_comm]
\end{verbatim}
\end{footnotesize}

    

\begin{proposition}[Centralizer of noncenter $d_\delta$]
    \label{centralizer_d_eq_D}
    \uses{SpecialMatrices.d, SpecialSubgroups.D}
    \lean{centralizer_d_eq_D}
    \leanok
    The centralizer $C_{\SL_2(F)}(d_\delta) = D$ for $\delta \neq \pm 1$.
    \end{proposition}        
\begin{proof}
    \uses{SpecialLinearGroup.fin_two_diagonal_iff}
    \leanok
Now we consider which $y \in \SL_2(F)$satisfy $y d_\delta = d_\delta y$ for an arbitrarily chosen $d_\delta$, with $\delta \neq \pm 1$.
\begin{align}\label{6.3proof4} y d_ \delta &= d_\sigma y, \nonumber \\[1.5ex]
\begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} &= \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix}, \nonumber \\[1.5ex]
\begin{bmatrix} \alpha \delta & \beta \delta^{-1} \\ \gamma \delta & \delta \delta^{-1} \end{bmatrix} &= \begin{bmatrix} \alpha \delta & \beta \delta \\ \gamma \delta^{-1} & \delta \delta^{-1} \end{bmatrix}.
\end{align}

Equating the top right and bottom left entries of (\ref{6.3proof4}) gives that $\beta = 0 = \gamma$ since Since $\delta \neq \delta^{-1}$. Thus $\delta = \alpha^{-1}$ and 
\begin{align*} x = \begin{bmatrix} \alpha & 0 \\ 0 & \alpha^{-1} \end{bmatrix} \in D. 
\end{align*}

Thus $x$ and indeed the whole of $C_{\SL_2(F)}(d_\delta)$ is contained in $D$. Now take an arbitrary $d_\rho \in D$.
\begin{align*} d_\rho d_\delta = d_{\rho \delta} = d_\delta d_\rho.
\end{align*}
So clearly $D \subset C_{\SL_2(F)}(d_\delta)$ and thus $C_{\SL_2(F)}(d_\delta) = D$ as required.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma centralizer_d_eq_D (δ : Fˣ) (δ_ne_one : δ ≠ 1) (δ_ne_neg_one : δ ≠ -1) :
    centralizer {d δ} = D F := by
    ext x
    constructor
    · intro hx
      simp [mem_centralizer_iff] at hx
      rcases get_entries x with ⟨a, b, c, d, _ha, hb', hc', _hd, x_eq⟩
      simp [SpecialLinearGroup.fin_two_ext_iff, SpecialMatrices.d, x_eq] at hx
      obtain ⟨-, hb, hc, -⟩ := hx
      have δ_ne_zero : (δ : F) ≠ 0 := Units.ne_zero δ
      have δ_ne_δ_inv : (δ : F) ≠ δ⁻¹ := by
        intro h
        rw [Field.self_eq_inv_iff _ δ_ne_zero] at h
        simp_rw [Units.val_eq_one, Units.val_eq_neg_one] at h
        absurd not_or.mpr ⟨δ_ne_one, δ_ne_neg_one⟩ h
        trivial
      rw [mul_comm, mul_eq_mul_left_iff] at hb hc
      replace hb := Or.resolve_left hb δ_ne_δ_inv
      replace hc := Or.resolve_left hc δ_ne_δ_inv.symm
      rw [mem_D_iff, ← SpecialLinearGroup.fin_two_diagonal_iff]
      simp [hb, hc, ← hb', ← hc']
    · rintro ⟨δ', rfl⟩
      simp [mem_centralizer_iff, mul_comm]
\end{verbatim}
\end{footnotesize}

%---------------------------------------------------


\begin{proposition}[Centralizers of conjugate elements]
    \label{conjugate_centralizers_of_IsConj}
    \lean{conjugate_centralizers_of_IsConj}
    \leanok
    Let $a$ and $b$ be conjugate elements in a group $G$. Then $\exists \, x \in G$ such that $xC_G(a)x^{-1} = C_G(b)$. \vspace{3mm}
\end{proposition}
\begin{proof}
\leanok
This proposition essentially claims that conjugate elements have conjugate centralizers. Since $a$ and $b$ are conjugate there exists an $x \! \in \! G$ such that $b = xax^{-1}$. Let $g$ be an arbitrary element of $C_G(a)$. Then,

\begin{align*} (xgx^{-1})(xax^{-1}) &= xgax^{-1}\\
&= xagx^{-1} \tag{since $g \in C_G(a)$}\\
&= (xax^{-1})(xgx^{-1}). \end{align*}

Thus $xgx^{-1} \in C_G(xax^{-1})$. Since $g$ was chosen arbitrarily, $$xC_G(a)x^{-1} \subset C_G(xax^{-1}) = C_G(b).$$ 

Conversely, let $h$ be an arbitary element of $C_G(xax^{-1})$. Then,

\begin{align*} (x^{-1}hx)a &= x^{-1}h(xax^{-1})x \\
&= x^{-1}(xax^{-1})hx \tag{since $h \in C_G(xax^{-1})$} \\
&= a(x^{-1}hx). \end{align*}

So $x^{-1}hx \in C_G(a)$ and since $h$ was arbitrarily chosen from $C_G(xax^{-1})$, \linebreak $x^{-1}C_G(xax^{-1})x \subset C_G(a)$. Multiplication on the left by $x$ and on the right by $x^{-1}$ gives $C_G(b) =  C_G(xax^{-1}) \subset xC_G(a)x^{-1}$. Since we have shown that each set contains the other, $xC_G(a)x^{-1} = C_G(b)$ as required. \\
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma conjugate_centralizers_of_IsConj  (a b : G) (hab : IsConj a b) :
    ∃ x : G, conj x • (centralizer { a }) = centralizer { b } := by
    rw [isConj_iff] at hab
    obtain ⟨x, hc⟩ := hab
    use x
    ext y
    simp [centralizer, MulAut.conj]
    constructor
    · rintro ⟨y', y'_in_cen, hy'⟩
      simp at hy' y'_in_cen ⊢
      rw [Set.mem_centralizer_iff]
      rintro m ⟨rfl⟩
      have : a * y' = y' * a := by exact y'_in_cen a rfl
      rw [← hy', ← hc]
      group
      rw [mul_assoc x a, this]
      group
    · intro hy
      simp [Set.mem_centralizer_iff] at hy
      have : y = b * y * b⁻¹ := by rw [hy]; group
      simp [← hc] at this hy
      use a * x⁻¹ * y * x * a⁻¹
      split_ands
      · simp
        rw [Set.mem_centralizer_iff]
        simp_rw [Set.mem_singleton_iff, forall_eq, inv_mul_cancel_right]
        nth_rewrite 1 [← mul_one a, ← inv_mul_cancel x]
        have helper: a * (x⁻¹ * x) * (a * x⁻¹ * y * x * a⁻¹) =
          a * x⁻¹ * (x * a * x⁻¹ * y * x * a⁻¹) := by group
        rw [helper, hy]
        group
      · simp
        group at hy ⊢
        rw [hy]
        group
\end{verbatim}
\end{footnotesize}



\begin{corollary}[ Centralizer of non-central element is commutative]
    \label{IsCommutative_centralizer_of_not_mem_center}
    \lean{IsCommutative_centralizer_of_not_mem_center}
    \leanok
The centralizer of an element $x$ in $\SL_2(F)$ is abelian unless $x$ belongs to the centre of $L$. \vspace{3mm}
\end{corollary}

\begin{proof}
    \uses{SL2_IsConj_d_or_IsConj_s_or_IsConj_neg_s_of_AlgClosed, conjugate_centralizers_of_IsConj, centralizer_s_eq_SZ, centralizer_d_eq_D}
    \leanok
    This is almost an immediate consequence of the preceding results. Propositions \ref{centralizer_s_eq_SZ} and \ref{centralizer_d_eq_D} show that an element of the form $\pm s_\sigma$ which does not lie in the centre of $\SL_2(F)$ has centralizer $S \times Z$, whilst a non-central element of the form $d_\delta$ has centralizer $D$.
Both $S$ and $D$ are abelian since they are isomoprhic to $F$ and $F^\times$ respectively. Let $s_\sigma z_1$ and $s_\gamma z_2$  be arbitrary elements of $\times Z$.

\vspace{-.5mm}
\begin{align*} 
    (s_\sigma z_1)(s_\gamma z_2)  &= s_\sigma s_\gamma z_2 z_1  \tag{since  $z_1 \in Z$}
\\ &= s_\gamma s_\sigma z_2 z_1  \tag{since  $T$ is abelian}
\\ &= (s_\gamma z_2)(s_\sigma z_1).   \tag{since  $z_2 \in Z$}
\end{align*} 

Thus $S \times Z$ is also abelian. Since every element of $\SL_2(F)$ is conjugate to $d_\delta$ or $\pm s_\sigma$ by Proposition \ref{SL2_IsConj_d_or_IsConj_s_or_IsConj_neg_s_of_AlgClosed} and conjugate elements have conjugate centralizers by Proposition \ref{conjugate_centralizers_of_IsConj}, the centralizer of each $x \in \SL_2(F)\setminus Z$ is conjugate to either $\times Z$ or $D$. 
Proposition \ref{conjugateprop}(iii) shows that conjugate subgroups are isomorphic and therefore have the same structure, thus since both $S \times Z$ and $D$ are abelian, $C_{\SL_2(F)}(x)$ is also abelian. 
Note that in general this does hold for $x \in Z$, since its centralizer is the whole of $\SL_2(F)$which is not abelian unless $\SL_2(F)= Z$.

\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma IsCommutative_centralizer_of_not_mem_center [IsAlgClosed F] [DecidableEq F](x : SL(2,F))
    (hx : x ∉ center SL(2,F)) : IsCommutative (centralizer { x }) := by
    rcases SL2_IsConj_d_or_IsConj_s_or_IsConj_neg_s_of_AlgClosed x with
      (⟨δ, x_IsConj_d⟩ | ⟨σ, x_IsConj_s⟩ | ⟨σ, x_IsConj_neg_s⟩ )
    · obtain ⟨x, centralizer_x_eq⟩ := conjugate_centralizers_of_IsConj (d δ) x x_IsConj_d
      have δ_ne_one : δ ≠ 1 := by
        rintro rfl
        simp at x_IsConj_d
        rw [← x_IsConj_d, center_SL2_eq_Z] at hx
        simp at hx
      have δ_ne_neg_one : δ ≠ -1 := by
        rintro rfl
        simp at x_IsConj_d
        rw [← x_IsConj_d, center_SL2_eq_Z] at hx
        simp at hx
      rw [← centralizer_x_eq, centralizer_d_eq_D _ δ_ne_one δ_ne_neg_one]
      exact map_isCommutative _ _
    · obtain ⟨x, centralizer_S_eq⟩ := conjugate_centralizers_of_IsConj (s σ) x x_IsConj_s
      have σ_ne_zero : σ ≠ 0 := by
        rintro rfl
        simp at x_IsConj_s
        rw [← x_IsConj_s, center_SL2_eq_Z] at hx
        simp at hx
      rw [← centralizer_S_eq, centralizer_s_eq_SZ σ_ne_zero]
      exact map_isCommutative _ _
    · obtain ⟨x, centralizer_S_eq⟩ := conjugate_centralizers_of_IsConj (-s σ) x x_IsConj_neg_s
      have σ_ne_zero : σ ≠ 0 := by
        rintro rfl
        simp at x_IsConj_neg_s
        rw [← x_IsConj_neg_s, center_SL2_eq_Z] at hx
        simp at hx
      rw [← centralizer_S_eq,  ← centralizer_neg_eq_centralizer, centralizer_s_eq_SZ σ_ne_zero]
      exact map_isCommutative _ _
\end{verbatim}
\end{footnotesize}



\section{The Projective Line \& Triple Transitivity}

It is convenient to sometimes take a geometric viewpoint and regard the elements of $\SL_2(F)$as pairs of vectors in the 2-dimensional vector space over $F$, which we will denote $V$. An element of $\SL_2(F)$is thus a linear transformation of $V$. 

\begin{definition} 
    Let $\mathscr{L}$ be the set of all 1-dimensional subspaces of $V$. A subset $\mathscr{S}$ of $\mathscr{L}$ is called a \textbf{subspace} of $\mathscr{L}$ if there is a subspace $U$ of $V$ such that $\mathscr{S}$ is the set of all 1-dimensional spaces of $U$. We have dim $U =$ dim $\mathscr{S} + 1$. The set $\mathscr{L}$ on which this concept of subspaces is defined is called the \textbf{projective line} on $V$ and an element of $\mathscr{L}$ is a 0-dimensional subspace of $\mathscr{L}$ and consequently called a \textbf{point}. The projective line can be considered as a straight line in the field, plus a point at infinity.
\end{definition}

Any 1-dimensional subspace of $V$ is a set of vectors of the form $\eta u$, where $u$ is a non-zero vector of $V$ and $\eta \in F^\times$. Thus the points of $\mathscr{L}$ are equivalence classes with the following relation defined on the set of vectors of $V$.
\begin{align*} u = \begin{bmatrix} u_1 \\ u_2 \end{bmatrix} \sim \begin{bmatrix} v_1 \\ v_2 \end{bmatrix} = v \iff u = \eta v, \qquad (\text{for $\eta \in F^\times$}).
\end{align*}

Notice that $u$ and $v$ are equivalent if and only if $u_1 v_2 = v_1 u_2$. Importantly each point $P_i$ of $\mathscr{L}$ can be represented by a corresponding equivalence class of vectors of $V$, that is, $P$ corresponds to $u$ if $P = u_1 / u_2$. In the case when $u_2 = 0$, this corresponds to the point at infinity.

\begin{definition} Let $S$ be a permutation group which acts on a set $X$ and $\{ x_1, x_2, x_3 \}$ and $\{ x_1', x_2', x_3' \}$ be two subsets of distinct elements of $X$. Then $S$ is said be \textbf{triply transitive} on $X$ if there is an element $\pi \in S$ such that,
\begin{align*} x^{\pi}_i = x'_i, \qquad(\text{$i$ = 1,2 or 3}).
\end{align*} 
\end{definition}

\begin{theorem} \label{6.6}
Let $\mathscr{L}$ be the projective line over the field $F$. Then $\SL_2(F)$is triply transitive on the set of the points of $\mathscr{L}$. \vspace{3mm}
\end{theorem}

\begin{proof} Let $P_1$, $P_2$ and $P_3$ be distinct points of $\mathscr{L}$ and $p_i$ be a vector in $V$ corresponding to $P_i$. Since each $P_i$ is distinct, $p_1$, $p_2$ and $p_3$ are thus pairwise linearly independent. Thus $p_1$ and $p_2$  form a basis for $V$ and it's clear that there exist $\alpha, \beta \in F^\times$ such that,
\begin{align*} p_3 = \alpha p_1 + \beta p_2.
\end{align*}

Now, let $Q_1$, $Q_2$ and $Q_3$ be three more distinct points of $\mathscr{L}$ and $q_i$ be a vector in $V$ corresponding to $Q_i$. Similarly, by the above argument, there exist $\gamma, \delta \in F^\times$ such that,
\begin{align*} q_3 = \gamma q_1 + \delta q_2.
\end{align*}

Let $\pi \in GL(2,F)$ be the linear transformation which sends $\alpha p_1$ to $\gamma q_1$  and $\beta p_2$ to $\delta q_2$. Thus,
\begin{align*} \pi(p_3) = \pi(\alpha p_1 + \beta p_2) = \pi(\alpha p_1) + \pi(\beta p_2) = \gamma q_1 + \delta q_2 = q_3 
\end{align*}

Hence we get $P^\pi_1 = Q_1$, $P^\pi_2 = Q_2$ and $P^\pi_3 = Q_3$ and $GL(2,F)$ is triply transitive. Now set,
\begin{align*} \eta = \sqrt{\frac{1}{\text{det }\pi}}.
\end{align*}

Consider the mapping $\theta$ which sends $\alpha p_1$ to $\eta \gamma q_1$ and $\beta p_2$ to $\eta \delta q_2$. Observe that,
\begin{align*} \text{det }\theta = \eta^2 \, \text{det } \pi = 1
\end{align*}

So $\theta \in SL(2,F) = \SL_2(F)$and since $P^\theta_1 = Q_1$, $P^\theta_2 = Q_2$ and $P^\theta_3 = Q_3$, we have that $\SL_2(F)$is also triply transitive. 

\end{proof}

The following proposition looks at what happens when the group $\SL_2(F)$acts on the projective line $\mathscr{L}$.

\begin{proposition} \label{6.7} (i) Each element of the form $d_\delta$ (with $\delta \neq \pm 1$), fixes the same two points on the projective line $\mathscr{L}$ and fix no other point. \vspace{3mm} \\
(ii) Each element of the form $\pm s_\sigma$ (with $\sigma \neq 0$), fixes the same point $P$ on $\mathscr{L}$ and fix no other point. Furthermore, \emph{Stab}$(P) = H$. \vspace{3mm} \\
(iii) All conjugate elements have the same number of fixed points on $\mathscr{L}$. \vspace{3mm} \\
(iv) Any noncentral element of $\SL_2(F)$has at most 2 fixed points on $\mathscr{L}$.
\end{proposition}

\begin{proof} 
(i) Let $P$ be a fixed a point of an arbitrary $d_\delta \in D$, with $\delta \neq \pm 1$ and let $u$ belong to the corresponding equivalence class of vectors of $V$ to $P$. \\
\begin{align*} d_\delta u = \begin{bmatrix} \delta & 0 \\ 0 & \delta^{-1} \end{bmatrix} \begin{bmatrix} u_1 \\ u_2 \end{bmatrix} &= \begin{bmatrix} u_1 \delta \\ u_2 \delta^{-1} \end{bmatrix} \sim \begin{bmatrix} u_1 \\ u_2 \end{bmatrix}, 
\\[1.5ex] u_1 u_2 \delta &= u_1 u_2 \delta^{-1}.
\end{align*}

Since $\delta \neq \pm 1$, $\delta$ does not equal $\delta^{-1}$, and so either $u_1 = 0$ or $u_2 = 0$. Thus $u$ is equivalent to either the vector $\begin{bmatrix} 0 \\ 1 \end{bmatrix}$ or $\begin{bmatrix} 1 \\ 0 \end{bmatrix}$ and these correspond to 2 distinct points of $\mathscr{L}$ which are fixed by $d_\delta$. \\
\\
(ii) Let $P$ be a fixed a point of an arbitrary $s_\sigma$, with $\sigma \neq 0$, and let $u$ be the corresponding element of $V$ to $P$. \\
\begin{align*} s_\sigma u = \begin{bmatrix} 1 & 0 \\ \sigma & 1 \end{bmatrix} \begin{bmatrix} u_1 \\ u_2 \end{bmatrix} &= \begin{bmatrix} u_1 \\ u_1 \sigma + u_2 \end{bmatrix} \sim \begin{bmatrix} u_1 \\ u_2 \end{bmatrix}, 
\\[1.5ex] u_1 u_2 &= {u_1}^2 \sigma + u_1 u_2.
\end{align*}

This gives ${u_1}^2 \sigma = 0$ and since $\sigma \neq 0$ we have $u_1 = 0$. Thus $s_\sigma$ has just one fixed point, $P$ which corresponds to the equivalence class of $\begin{bmatrix} 0 \\ 1 \end{bmatrix}$ in $V$. We show also that $P$ is also the only fixed point of $-s_\sigma$, with $\sigma \neq 0$.
\begin{align*} -s_\sigma u = \begin{bmatrix} -1 & 0 \\ \sigma & -1 \end{bmatrix} \begin{bmatrix} u_1 \\ u_2 \end{bmatrix} &= \begin{bmatrix} -u_1 \\ u_1 \sigma - u_2 \end{bmatrix} \sim \begin{bmatrix} u_1 \\ u_2 \end{bmatrix}, 
\\[1.5ex] -u_1 u_2 &= {u_1}^2 \sigma - u_1 u_2.
\end{align*}

So again $u_1 =0$ and $-s_\sigma$ fixes $P$ and no other point. We now calculate the stabiliser of $P$ in $L$, by considering which $x \in \SL_2(F)$fix $P$. \\
\begin{align*} x u = \begin{bmatrix} \alpha & \beta \\ \gamma & \delta \end{bmatrix} \begin{bmatrix} 0 \\ 1 \end{bmatrix} &= \begin{bmatrix} \beta \\ \delta \end{bmatrix} \sim \begin{bmatrix} 0 \\ 1 \end{bmatrix}.
\end{align*}

Thus $\beta = 0$ and $x \in H$. Since $x$ was chosen arbitrarily from Stab$(P)$, we have Stab$(P) \subset H$. Now let an arbitrarily chosen $y \in H$ act on $P$. \\
\begin{align*} y u = \begin{bmatrix} \alpha & 0 \\ \gamma & \alpha^{-1} \end{bmatrix} \begin{bmatrix} 0 \\ 1 \end{bmatrix} &= \begin{bmatrix} 0 \\ \alpha^{-1} \end{bmatrix} \sim \begin{bmatrix} 0 \\ 1 \end{bmatrix}.
\end{align*}

Thus $y$ and indeed $H$ is contained in Stab$(P)$, so Stab$(P) = H$ as desired. \\
\\
(iii) Let $P_i$ $(i = 1,2,...)$ be the fixed points of $x\in \SL_2(F)$and let $y$ be conjugate to $x$ in $L$. That is, there exists a $g \in \SL_2(F)$such that $x = gyg^{-1}$.
\begin{align*} x P_i &= P_i,
\\ gyg^{-1} P_i &= P_i,
\\ y(g^{-1} P_i) &= (g^{-1} P_i).
\end{align*}

This shows that $P_i$ is a fixed point of $x$ if and only if $g^{-1} P_i$ is a fixed point of $y$. Thus conjugate elements have the same number of fixed points. \\
\\
(iv) By Proposition \ref{6.3}(i), every  element of $\SL_2(F)$is conjugate to either $d_\delta$ or $\pm s_\sigma$, so since conjugate elements have the same number of fixed points, every element of $\SL_2(F)\! \setminus \! Z$ has either the same number of fixed points as $d_\delta$ (with $\delta \neq \pm 1$), namely 2, or the same number as $\pm s_\sigma$, (with $\sigma \neq 0$), namely 1.

\end{proof}


