\chapter{Reduction of classification of finite subgroups of $\PGL_2(\Fbar_p)$ to classification of finite subgroups of $\PSL_2(\Fbar_p)$}\label{Ch4_ReductionOfProblem}

\section{Over an algebraically closed field $\PSL_n(F)$ is isomorphic to the projective $\PGL_n(F)$}


When $F$ is algebraically closed and $\textrm{char}(F) \neq 2$ it one can construct an isomorphism between 
the projective special linear group and the projective general linear group.

\begin{definition}
\label{SL_monoidHom_PGL}
\lean{SL_monoidHom_PGL}
\leanok
    Let $\varphi : \SL_n(R) \rightarrow \PGL_n(R)$ be the injection of $\PSL_n(R)$ into $\PGL_n(R)$ defined by
    \[
     S \mapsto i(S) \;  (R^\times I) 
    \]

    where $i : \SL_n(F) \hookrightarrow \GL_n(F)$ is the natural injection of the special linear group into the general linear group.
\end{definition}
\begin{footnotesize}
\begin{verbatim}
  def SL_monoidHom_PGL (n : Type*) [Fintype n] [DecidableEq n] (R : Type*) [CommRing R] :
  SL n R →*  PGL n R := (GL_monoidHom_PGL n R).comp (SL_monoidHom_GL n R)
\end{verbatim}
\end{footnotesize}


We now prove a useful fact about elements that belong to the center of $\GL_n(R)$.

\begin{lemma}
    \label{GeneralLinearGroup.mem_center_general_linear_group_iff}
    \lean{GeneralLinearGroup.mem_center_general_linear_group_iff}
    \leanok
     Let $R$ be a commutative ring, then $G \in GL_n(F)$ belongs to center of $\GL_n(R)$, $Z(\GL_n(R))$ if and only if $G = r \cdot I$ where $r \in R^\times$.
    \end{lemma}
    
    \begin{proof}
        \leanok
        \begin{itemize}
        \item Suppose $G \in \GL_n(F)$ belongs to $Z(\GL_n(F))$ then for all $H \in \GL_n(F)$ we have that $G H = H G$. We will find it sufficient to only consider the case where $H$ is a transvection matrices.
        Let $1 \leq i < j \leq n$, then the transvection matrices are of the form $T_{ij} = I + E_{ij}$ where $E_{ij}$ is the standard basis matrix given by
        \[
        E_{{ij}_{kl}} = \begin{cases}
        1 & \text{if $i = k$ and $l = j$}\\
        0 & \text{otherwise}
        \end{cases}
        \] 
    
        Given $T_{ij} G = (I + E_{ij}) G = G T_{ij} (I + E_{ij})$, and addition is commutative we can use the cancellation law to yield that
        
        \[
        E_{ij} G = G E_{ij}
        \]
    
        But $G$ only commutes with $E_{ij}$ for all $i \neq j$ if $G = r \cdot I$ for some $r \in R^\times$.
        
        \item Suppose $G = r \cdot I$ for some $r \in R^\times$ then it is clear that for all $H \in \GL_n(F)$ that $r \cdot I  H = r \cdot H = H \cdot r = H (r \cdot I)$
        \end{itemize}
    \end{proof}
\begin{footnotesize}
\begin{verbatim}
theorem mem_center_general_linear_group_iff {n : Type u} [DecidableEq n]
    [Fintype n] {R : Type*} [CommRing R] {M : GL n R} :
    M ∈ center (GL n R) ↔ (∃ r : Rˣ, (r • 1) = M) := by
    rw [mem_center_iff]
    refine ⟨?mp, ?mpr⟩
    case mp =>
      intro hM
      -- If M commutes with every matrix then it must commute with the transvection matrices
      have h₀ : ∀ (t : TransvectionStruct n R), Commute (t.toGL) M := fun t => hM t.toGL
      /-
      If M commutes with the transvection matrices,
      then M ∈ Set.range (Matrix.scalar n) where Set.range is Rˣ
      -/
      simp_rw [← Commute.units_val_iff] at h₀
      have h₁ : (M : Matrix n n R) ∈ Set.range ⇑(Matrix.scalar n) :=
        mem_range_scalar_of_commute_transvectionStruct h₀
      obtain ⟨r, rfl⟩ :=
        mem_range_unit_scalar_of_mem_range_scalar_and_mem_general_linear_group h₁
      use r
    case mpr =>
      intro hM N
      obtain ⟨r, rfl⟩ := hM
      ext i j
      simp [GeneralLinearGroup.coe_mul, GeneralLinearGroup.coe_mul,
        ← coe_scalar_matrix, scalar_commute]
\end{verbatim}
\end{footnotesize}

\begin{lemma}
\label{center_SL_le_ker}
\uses{SL_monoidHom_PGL}
\lean{center_SL_le_ker}
\leanok
Let $R$ be a non-trivial commutative ring, then $Z(\SL_n(R)) \le \ker (\varphi)$.
\end{lemma}
\begin{proof}
\uses{GeneralLinearGroup.mem_center_general_linear_group_iff}
\leanok
If $S \in Z(\SL_n(R)) \leq \SL_n(F)$ then $S = \omega I$ where $\omega$ is a primitive root of unity.

Because $\varphi = \pi_{Z(\GL_n(F))} \circ i$, the kernel of $\varphi$ is $i^{-1}(Z(\GL_n(F)))$, where we recall that $i : \SL_n(R) \hookrightarrow \GL_n(F)$ is the injection of $SL_n(F)$ into $\GL_n(F)$.

But given $i(S) = i(\omega \cdot I) = \omega \cdot I$ is of the form $r \cdot I$ where $r \in R^\times$ by \ref{GeneralLinearGroup.mem_center_general_linear_group_iff} it follows that $S \in \ker \varphi$, as desired.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma center_SL_le_ker (n : Type*) [Fintype n] [DecidableEq n]
    (R : Type*) [CommRing R]:
    center (SpecialLinearGroup n R) ≤ (SL_monoidHom_PGL n R).ker := by
    cases hn : isEmpty_or_nonempty n
    · exact le_of_subsingleton
    · intro x x_mem_center
      rw [SpecialLinearGroup.mem_center_iff] at x_mem_center
      obtain ⟨ω, hω, h⟩ := x_mem_center
      simp [MonoidHom.mem_ker, SL_monoidHom_PGL, GL_monoidHom_PGL, SL_monoidHom_GL]
      rw [GeneralLinearGroup.mem_center_general_linear_group_iff]
      have IsUnit_ω : IsUnit ω := IsUnit.of_pow_eq_one hω Fintype.card_ne_zero
      use IsUnit_ω.unit
      ext
      simp only [coe, ← h, scalar_eq_smul_one]
      rfl
\end{verbatim}
\end{footnotesize}



\begin{definition}
\label{PSL_monoidHom_PGL}
\uses{SL_monoidHom_PGL}
\lean{PSL_monoidHom_PGL}
\leanok
    Given $Z(\SL_n(F)) \leq \ker \varphi$ as shown in \ref{center_SL_le_ker}, by the universal property there exists a unique homomorphism $\bar{\varphi} : \PSL_n(F) \rightarrow \PGL_n(F)$ which is the lift of $\varphi$. 
    
    Where $\varphi = \bar{\varphi} \circ \pi_{Z(\SL_n(F))}$ and $\pi_{Z(\SL_n(F))} : \SL_n(F) \rightarrow \PSL_n(F)$ is the canonical homomorphism from the group into its quotient.
\end{definition}
\begin{footnotesize}
\begin{verbatim}
def PSL_monoidHom_PGL (n R : Type*) [Fintype n] [DecidableEq n] [CommRing R] :
    PSL n R →* PGL n R :=
    @QuotientGroup.lift (SL n R) _ (center (SL n R)) (center_is_normal n R) (PGL n R)
      (PGL_is_monoid n R) (SL_monoidHom_PGL n R) (center_SL_le_ker n R)
  
\end{verbatim}
\end{footnotesize}


\begin{lemma}
\label{Injective_PSL_monoidHom_PGL}
\lean{Injective_PSL_monoidHom_PGL}
\uses{PSL_monoidHom_PGL}
\leanok
    The homomorphism $\bar{\varphi}$ is injective.
\end{lemma}
\begin{proof}
\uses{GeneralLinearGroup.mem_center_general_linear_group_iff}
\leanok

To show $\bar{\varphi}$ is injective we must show that $\ker \bar{\varphi} \leq \bot_{\PSL_n(F)}$ where $\bot_{\PSL_n(F)}$ is the trivial subgroup of $\PSL_n(F)$.

Let $[S] \in \PSL_n(F)$ and suppose $[S] \in \ker \bar{\varphi}$. If $[S] \in \ker \bar{\varphi}$ then $\bar{\varphi} ([S]) = [1]_{\PGL_n(F)}$. But on the other hand, $\bar{\varphi} ([S]) = \varphi(s)$ and so $\varphi(S) = 1_{\PGL_n(F)}$, 
and thus $S \in Z(\GL_n(F))$, from \ref{GeneralLinearGroup.mem_center_general_linear_group_iff} it follows that $s = r \cdot I$ for some $r \in R^\times$. But given $S \in \SL_n(F)$ we know that 

\begin{equation*}
    \det(S) = \det(r \cdot I) = r^n \cdot 1 = 1 \implies \text{$r$ is a $n$th root of unity}
\end{equation*}

Therefore, given elements of $Z(\SL_n(F))$ are those matrices of the form $\omega \cdot I$ where $\omega$ is a $n$th root of unity, we can conclude that $[S] = [1]_{\PSL_n(F)}$ and thus $\ker \bar{\varphi} \leq \bot_{\PSL_n(F)}$ as required.

Which shows that the homomorphism $\bar{\varphi}$ is injective.
\end{proof}
\begin{footnotesize}
    \begin{verbatim}
    theorem Injective_PSL_monoidHom_PGL  (n F : Type*) [hn₁ : Fintype n] [DecidableEq n]
        [Field F] [IsAlgClosed F] :  Injective (PSL_monoidHom_PGL n F) := by
        rw [← MonoidHom.ker_eq_bot_iff, eq_bot_iff]
        intro psl psl_in_ker
        obtain ⟨S, hS⟩ := Quotient.exists_rep psl
        rw [← hS] at psl_in_ker
        simp only [PSL_monoidHom_PGL, SL_monoidHom_PGL, GL_monoidHom_PGL, SL_monoidHom_GL,
          MonoidHom.mem_ker, QuotientGroup.lift_mk, MonoidHom.coe_comp, QuotientGroup.coe_mk',
          Function.comp_apply, QuotientGroup.eq_one_iff] at psl_in_ker
        rw [GeneralLinearGroup.mem_center_general_linear_group_iff] at psl_in_ker
        obtain ⟨ω, hω⟩ := psl_in_ker
        have ω_eq_root_of_unity : det S.val = 1 := SpecialLinearGroup.det_coe S
        simp [GeneralLinearGroup.ext_iff, SpecialLinearGroup.toGL] at hω
        have S_eq_omega_smul_one : (S : Matrix n n F) = ω • 1 := Eq.symm (Matrix.ext hω)
        simp [S_eq_omega_smul_one] at ω_eq_root_of_unity
        simp [← hS]
        refine SpecialLinearGroup.mem_center_iff.mpr ?_
        use ω
        refine ⟨?ω_is_root_of_unity, ?S_is_scalar_matrix⟩
        case ω_is_root_of_unity => exact (eq_one_iff_eq_one_of_mul_eq_one ω_eq_root_of_unity).mpr rfl
        case S_is_scalar_matrix => rw [S_eq_omega_smul_one]; exact scalar_eq_smul_one n F ↑ω
    \end{verbatim}
    \end{footnotesize}
    
    \begin{remark}[Quotients and their maps in Lean]
    When formalising results on quotient groups or for that matter any quotient type, it is valuable to appreciate which model Lean uses for quotients. 
    
    Typically, when one thinks of the elements of the quotient group say $\Z/2\Z$ there are two elements: 
    $[0]$ which represents the coset $\{\ldots, -2, 0, 2, \ldots\}$, and $[1]$ which represents the coset $\{\ldots, -3, -1, 1, 3, \ldots\}$
    since under the equivalence relation, $a \sim b$ if and only if $a - b \in 2\Z$. In this new setting all the elements belonging to the same coset, 
    or equivalence class, are now considered to be indistinguishable.

    Similarly, when defining a group homomorphism from $\theta : (\Z /2\Z, \dot{+}) \rightarrow G$, under this model one has to make sure that
    all the elements in $[0]$ are sent to the same element $g \in G$ in the target; and likewise, all the elements of $[1]$ are sent to the same element $h \in H$.

    Otherwise, should $\theta([0]) \ne \theta([2])$ then this would mean that $\theta$ would be treating what we thought were the indistinguishable elements $0$ and $2$, as different,
    This is the idea of showing the \textit{well-definedness} of a map on a quotient.

    In general, one of the idea of quotients (not only group quotients) is to somehow eliminate redundant information.

    Let us run with the following amusing example in every day life:
    
    Suppose the lights are on in a room and suppose Bob asks Alice what would happen should he press the light switch $n \in \N$ times. It then occurs to
    Alice that in this setting pressing the light switch on $12$ times or $1400$ times, or for that matter, any even number of times yields to indistinguishable outcomes,
     the lights will be on; so in a sense the elements belonging to the set of even numbers are indistinguishable from each other, 
     what is more is that we are not interested in so much the number but the parity of the number.

    In this particular example, we defined a map $\psi : \N \rightarrow \{\text{On}, \; \text{Off}\}$ where we realised
    that both $12$ and $1400$ and all even natural numbers seem to behave equivalently under this map if and only if their difference is an even integer,
    that is, $a \sim b$ if and only if $a - b \in 2\Z$ where $a$ and $b$ are promoted to being elements of $\N \subset \Z$.    
    
    Given this map $\psi$ behaves the same on all elements which are indistinguishable, it seems natural to 
    define a map which now takes in the only relevant information which determines if the lights are on or not,
    the parity of the number of times the light switch has been pressed.
    
    \[
    \bar{\psi} : \N / \sim \rightarrow \{\text{On}, \; \text{Off}\}
    \]

    The quotient on $\N$ now allows us to say treat $[12] = [1400]$ and our new map $\bar{\psi}$ now recognises that these numbers are the same
    under this new light. 

    However, we could have also phrased this observation as saying that $\psi$ respects the equivalence relation on $\sim$ and
    thus have defined a $\bar{\psi}$ to be the map which given the parity, an element of $\N / \sim = \{[0], [1]\}$ outputs whether the light is on or off. 

    The upshot of all of this is that when we define a quotient and a map from a quotient, we ultimately want such a function to respect the equivalence relation. Whether the elements of a quotient are
    modelled as a coset, a set of equivalent elements, or as an abstract object which satisfies our needs should not be the main concern. 

    This is what the definition of quotients in Lean recognises, but it also recognises that it would be rather strange to think of a term of a quotient type as a set, since it would be rather clunky to work with; 
    instead one simply modifies the definition of equality, and in particular, when wanting to define in Lean the lift of an existing homomorphism $\gamma : G \rightarrow H$
    to $\bar{\gamma} : G/N \rightarrow H$, the most natural way to define/verify such a lift is sensible in Lean is to prove that equivalent elements map to the same output under $\gamma$.

    In fact, this is exactly what the general \texttt{Quot.lift} does:

    \begin{footnotesize}
    \begin{verbatim}
        Quot.lift.{u, v} 
          {α : Sort u} {r : α → α → Prop} {β : Sort v} 
        (f : α → β) (a : ∀ (a b : α), r a b → f a = f b) : Quot r → β
    \end{verbatim}
    \end{footnotesize}

    Similarly, \texttt{QuotientGroup.lift}, the universal property for factor groups, corresponds to:

    \begin{footnotesize}
    \begin{verbatim}
        QuotientGroup.lift.{u, x} {G : Type u} [Group G] (N : Subgroup G) 
          [nN : N.Normal] {M : Type x} [Monoid M] (φ : G →* M)
          (HN : N ≤ φ.ker) : G ⧸ N →* M
    \end{verbatim}
    \end{footnotesize}

    From this last definition one can see that there is no trace whatsoever to cosets. It is still possible to formalise a such a
    definition in a way that is akin to the notion of well-definedness, which is closer to the model of quotients as sets of subsets,
    since one can for example invoke \texttt{Quotient.exists_rep} which states:

    \begin{footnotesize}
    \begin{verbatim}
        Quotient.exists_rep.{u} 
          {α : Sort u} {s : Setoid α} (q : Quotient s) : ∃ a, ⟦a⟧ = q
    \end{verbatim}
    \end{footnotesize}    
    
    In fact, some of the theorems and definitions below heavily rely on this notion.
    Yet it becomes extremely useful later on to come to terms with this model of quotients and their maps which
    hinges on the universal property.
    \end{remark}

Before we can show that $\bar{\varphi}$ is surjective we need the following
lemma which allows us to find a suitable representative for an arbitrary element of $\PGL_n(F)$.

\begin{lemma}
\label{exists_SL_eq_scaled_GL_of_IsAlgClosed}
\lean{exists_SL_eq_scaled_GL_of_IsAlgClosed}
\leanok
If $F$ is an algebraically closed field then for every $G \in \GL_n(F)$ there exists a nonzero constant $\alpha \in F^\times$ and an element $S \in \SL_n(F)$ such that 
\begin{equation*}
    G = \alpha \cdot S
\end{equation*}
\end{lemma}

\begin{proof}
\leanok
Let $G \in \GL_n(R)$ then define
\begin{equation*}
    P(X) := X^n - \det(G)
\end{equation*}

By assumption $F$ is algebraically closed and $\det(G) \in F^\times$ thus there exists a root $\alpha \in F^\times$ such that 

\begin{equation*}
    \alpha^n - \det(G) = 0 \iff \alpha = \sqrt[n]{\det(G)} 
\end{equation*}

Let $S = \frac{1}{\alpha} \cdot G$, by construction $S \in \SL_n(F)$ as 

\begin{equation*}
    \det(S) = \left(\frac{1}{\alpha^n}\right) \cdot \det(G) = \frac{1}{\det(G)} \det(G) = 1
\end{equation*}
\end{proof}
\begin{footnotesize}
\begin{verbatim}
lemma exists_SL_eq_scaled_GL_of_IsAlgClosed {n F : Type*} [hn₁ : Fintype n] [DecidableEq n]
    [Field F] [IsAlgClosed F] (G : GL n F) :
    ∃ α : Fˣ, ∃ S : SL n F, α • S.toGL = G := by
    cases hn : isEmpty_or_nonempty n
    · exact ⟨1, 1, Subsingleton.elim _ _⟩
    let P : F[X] := X^(Nat.card n) - C (det G.val)
    have nat_card_ne_zero : (Nat.card n) ≠ 0 :=
      Nat.card_ne_zero.mpr ⟨inferInstance, inferInstance⟩;
    have deg_P_ne_zero : degree P ≠ 0 := by
      rw [Polynomial.degree_X_pow_sub_C]
      exact Nat.cast_ne_zero.mpr nat_card_ne_zero
      exact Nat.card_pos
    obtain ⟨α, hα⟩ := @IsAlgClosed.exists_root F _ _ P deg_P_ne_zero
    have c_ne_zero : α ≠ 0 := by
      rintro rfl
      simp [P] at hα
      have det_G_ne_zero : det G.val ≠ 0 := IsUnit.ne_zero <| isUnits_det_units G
      contradiction
    have IsUnit_c : IsUnit α := by exact Ne.isUnit c_ne_zero
    have hα' : ∃ c : Fˣ, c^(Nat.card n) = det G.val := by
      use IsUnit_c.unit
      simp [P, sub_eq_zero] at hα
      simp [hα]
    obtain ⟨α, hα⟩ := hα'
    have det_inv_c_G_eq_one : det (α⁻¹ • G).1 = (1 : F) := by
      simp [← hα, inv_smul_eq_iff, Units.smul_def]
    use α, ⟨(α⁻¹ • G), det_inv_c_G_eq_one⟩
    ext
    simp [coe]
\end{verbatim}
\end{footnotesize}

\begin{lemma}
\label{Surjective_PSL_monoidHom_PGL}
\uses{PSL_monoidHom_PGL}
\lean{Surjective_PSL_monoidHom_PGL}
\leanok
    The map $\bar{\varphi}$ is surjective.
\end{lemma}
\begin{proof}
\uses{exists_SL_eq_scaled_GL_of_IsAlgClosed}
\leanok
    Let $G \; (F^\times I) = [G] \in \PGL_n(F)$, then $G \in \GL_n(F)$ we can find a representative of $[G']$, that lies within the special linear group.
    Given elements of the special linear group are matrices with determinant equal to one, we must scale $G$ to a suitable factor to yield a representative which lies within $\SL_n(F)$. Suppose $\det(G) \ne 1$ and let
    \[
    P(X) := X^n - \det(G) \in F[X]
    \]
    By assumption, $F$ is algebraically closed so there exists a root $\alpha \ne 0\in F$ such that 
    \[
    \alpha^n - \det(G) = 0 \iff \alpha^n = \det(G)
    \]
    We can define
    \[
    G' := \frac{1}{\alpha} \cdot G \quad \text{where} \quad \det(G') = \frac{1}{\alpha^n} \det(G) = 1.
    \]
    Thus $G' \in \SL_n(F) \leq \GL_n(F)$ and given $G' = \frac{1}{\alpha} G$ we have that $G'  \; (F^\times I) = G \; (F^\times I)$.
    
    Therefore, $\varphi(G') = i(G') (F^\times I) = G' (F^\times I) = G (F^\times I)$.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
theorem Surjective_PSL_monoidHom_PGL (n F : Type*) [hn₁ : Fintype n] [DecidableEq n]
    [Field F] [IsAlgClosed F] :  Surjective (PSL_monoidHom_PGL n F) := by
    intro pgl
    obtain ⟨G, hG⟩ := Quotient.exists_rep pgl
    obtain ⟨c, S, h⟩ := exists_SL_eq_scaled_GL_of_IsAlgClosed G
    use (SL_monoidHom_PSL n F) S
    have class_G_eq_class_S : (⟦G⟧ : PGL n F) = ⟦S.toGL⟧ := by
      rw [Quotient.eq, QuotientGroup.leftRel_apply,
        GeneralLinearGroup.mem_center_general_linear_group_iff]
      use c⁻¹
      suffices c⁻¹ • 1 * G = S by
        simp [← h]
        ext
        simp [GeneralLinearGroup.scalar,
          nonsing_inv_mul _ <| isUnits_det_units (SpecialLinearGroup.toGL S),
          ← Matrix.smul_one_eq_diagonal]
      rw [← h, smul_mul_assoc, one_mul, inv_smul_eq_iff]
    rw [← hG, class_G_eq_class_S]
    -- by definition these equivalence classes are the same
    rfl
\end{verbatim}
\end{footnotesize}

\begin{lemma}
\label{Bijective_PSL_monoidHom_PGL}
\uses{PSL_monoidHom_PGL}
\lean{Bijective_PSL_monoidHom_PGL}
\leanok
    The map $\bar{\varphi}$ is bijective
\end{lemma}
\begin{proof}
\uses{Injective_PSL_monoidHom_PGL, Surjective_PSL_monoidHom_PGL}
\leanok
 We have shown that $\bar{\varphi}$ is injective in \ref{Injective_PSL_monoidHom_PGL} and have shown that $\bar{\varphi}$ is surjective in \ref{Surjective_PSL_monoidHom_PGL}. 
 Therefore, $\bar{\varphi}$ defines a bijection from $\PSL_n(F)$ to $\PGL_n(F)$.
\end{proof}
\begin{footnotesize}
\begin{verbatim}
theorem Bijective_PSL_monoidHom_PGL (n F : Type*) [hn₁ : Fintype n] [DecidableEq n]
    [Field F] [IsAlgClosed F] :  Bijective (PSL_monoidHom_PGL n F) := by
    refine ⟨?injective, ?surjective⟩
    case injective => exact Injective_PSL_monoidHom_PGL n F
    case surjective => exact Surjective_PSL_monoidHom_PGL n F
\end{verbatim}
\end{footnotesize}

\begin{theorem}
\label{PGL_iso_PSL}
\uses{PSL_monoidHom_PGL}
\lean{PGL_iso_PSL}
\leanok
    If $F$ is an algebraically closed field, then the map $\bar{\varphi} : \PSL_n(F) \rightarrow \PGL_n(F)$ defines a group isomorphism between $\PSL_n(F)$ and $\PGL_n(F)$.
\end{theorem}

\begin{proof}
\uses{Bijective_PSL_monoidHom_PGL}
\leanok
    The map $\bar{\varphi}$ was shown to be a bijection in \ref{Bijective_PSL_monoidHom_PGL} and given $\bar{\varphi}$ is mulitplicative as it was defined to be the lift of the homomorphism $\varphi$, we can conclude that 
    $\bar{\varphi}$ defines a group isomorphism between $\PSL_n(F)$ and $ºPGL_n(F)$
\end{proof}
\begin{footnotesize}
\begin{verbatim}
noncomputable def PGL_iso_PSL (n F : Type*) [Fintype n] [DecidableEq n] [Field F]
    [IsAlgClosed F] : PSL n F ≃* PGL n F :=
      MulEquiv.ofBijective (PSL_monoidHom_PGL n F) (Bijective_PSL_monoidHom_PGL n F)
\end{verbatim}
\end{footnotesize}

\begin{remark}[Noncomputable]
    Observe in the definition above it was necessary to add the \texttt{noncomputable} keyword before the definition, the reason for this is
    because the result \texttt{MulEquiv.ofBijective} implicitly uses the axiom of choice which means it is not possible for Lean to generate
    executable code.
\end{remark}



% \begin{center}
% \begin{tikzcd}
% 	{\SL_n(F)} && {\SL_n(F)} \\
% 	&& {} \\
% 	{\PSL_n(F)} && {\PGL_n(F)}
% 	\arrow["i", from=1-1, to=1-3]
% 	\arrow["{\textrm{can}_{\langle-I\rangle}}"', from=1-1, to=3-1]
% 	\arrow["{\textrm{can}_{F^\times I}}", from=1-3, to=3-3]
% 	\arrow[dotted, from=3-1, to=3-3]
% \end{tikzcd}
% \end{center}
% \end{proof}

This isomorphism will be essential to the classification of finite subgroups of $\PGL_2(\bar{\F}_p)$, as we only need understand a the classification of subgroups of $\PSL_2(\Fbar_p)$ to reach the desired result.


\section{Christopher Butler's exposition}

Following from the isomorphism defined in the previous section, we can now proceed to classify the finite subgroups of $\PGL_2(\bar{\F}_p)$ by classifying the finite subgroups of $\PSL_2(\bar{\F}_p)$. 
In turn, one can begin classifying the finite subgroups of $\PSL_2(\Fbar_p)$ by classifying the finite subgroups of $\SL_2(\Fbar_p)$ and then considering what happens after
quotienting by the center, $Z(\SL_2(F)) = \langle -I\rangle$.

We now turn our attention to the more general setting when $F$ is an arbitrary field that is algebraically closed, as this will turn out to be sufficient for our purposes.

Given $|\langle -I \rangle| = 2$ when $\textrm{char} F \ne 2$; and $\langle -I\rangle = \bot$ when $\textrm{char} F = 2$.
When a finite subgroup of $\SL_2(F)$ is sent through the canonical mapping $\pi_{Z(\SL_2(F))} : \SL_2(F) \rightarrow \PSL_2(F)$ 
the resulting subgroup will either shrink by a factor of two or it will remain intact should the center not be contained within the subgroup. 

We now proceed to classify all finite subgroups of $\SL_2(F)$ when $F$ is algebraically closed field. 
From now on, we follow Christopher Butler's exposition of Dickson's classification of finite subgroups of $\SL_2(F)$ over an algebraically closed field $F$. 

Christopher has been kind enough to provide the TeX code so I could prepare this blueprint which crucially hinges on the result which his exposition covers.